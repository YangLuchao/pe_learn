[toc]

俗话说：“工欲善其事，必先利其器。”本章将完成与Windows PE有关的三个小工具的开发。这三个小工具分别是：

PEDump：PE文件字节码查看器

PEComp：PE文件比较器

PEInfo：PE文件结构查看器

首先，让我们从编写最基本的汇编窗口程序开始。该窗口程序是编写本章的三个小工具的基础，也是编写后续大部分章节中其他程序的基础。

# 2.1　构造基本窗口程序

本节我们将构造一个具有基本窗口元素（含标题栏、菜单栏、工作区域）的窗口程序，后续大部分的程序开发都将以这个基本窗口程序作为基础进行扩展。

## 2.1.1　构造窗口界面

要构造的窗口程序具备窗口图形界面的大部分元素，包含窗口、菜单、图标、工作区域等。通常的做法是：首先，根据程序功能对程序的界面进行构思；然后，在纸上画出大致的结构图；最后，通过资源脚本来定义并实现界面中的每一部分。当然，读者也可以使用一些辅助的软件（如RADAsm中的资源编辑器，或者VS中的资源编辑器）根据构思好的界面，在所见即所得的资源编辑器图形界面中直接构造程序窗口界面。该程序最终显示的效果如图2-1所示。

![image](https://github.com/YangLuchao/img_host/raw/master/20230725/image.5uoi2kug7k80.webp)

图　2-1　基本窗口界面

## 2.1.2　编写相关的资源文件

构造完窗口界面以后，需要依据界面编写对应的资源文件，一般以".rc"为扩展名；这有点类似于工程建设里的依照图纸施工。资源文件编写完成后，还必须通过资源编译器对资源文件实施编译，以生成资源目标文件。资源文件是文本文件，由定义资源的一些脚本语句组成，可以使用文本编辑软件（如记事本）查看和修改。资源目标文件是对这些脚本的一种再组织，根据脚本描述将脚本涉及的所有资源编译到一起，形成二进制字节码。资源目标文件无法通过文本编辑软件查看。

整个过程分为两个阶段：

创建资源文件pe.rc

生成资源目标文件pe.res

下面分别来介绍这两个阶段的内容。

### 1.创建资源文件pe.rc

在编写资源文件时，需要定义图形中出现的所有菜单项、对话框、图标等。资源文件的详细编码如代码清单2-1所示。

代码清单2-1　资源文件的详细编码（chapter2\pe.rc）

------

```assembly
#include <resource.h>

#define ICO_MAIN  1000
#define DLG_MAIN  1000
#define IDC_INFO  1001
#define IDM_MAIN  2000
#define IDM_OPEN  2001
#define IDM_EXIT  2002

#define IDM_1    4000
#define IDM_2    4001
#define IDM_3    4002
#define IDM_4    4003


ICO_MAIN  ICON  "main.ico"

DLG_MAIN DIALOG 50,50,544,199
STYLE DS_MODALFRAME | WS_POPUP | WS_VISIBLE | WS_CAPTION | WS_SYSMENU
CAPTION "PE文件基本信息 by qixiaorui"
MENU IDM_MAIN
FONT 9,"宋体"
BEGIN
   CONTROL "",IDC_INFO,"RichEdit20A",196 | ES_WANTRETURN | WS_CHILD | ES_READONLY
               | WS_VISIBLE |WS_BORDER | WS_VSCROLL | WS_TABSTOP,0,0,540,396
END

IDM_MAIN menu discardable
BEGIN
  POPUP "文件(&F)"
  BEGIN
    menuitem "打开文件(&O)...",IDM_OPEN
    menuitem separator
    menuitem "退出(&x)",IDM_EXIT
  END

  POPUP "查看"
  BEGIN
    menuitem "源文件",IDM_1
    menuitem "窗口透明度",IDM_2
    menuitem separator
    menuitem "大小",IDM_3
    menuitem "宽度",IDM_4
  END

END
 
```



------

第1～13行定义了各元素常量，第16行指定了显示在窗口标题栏的图标为main.ico。需要注意的是，必须保证图标文件与资源文件在同一个目录中，如果指定的图标文件在其他目录中，则需要使用绝对路径，语法如下：

------

ICO_MAIN ICON"C:\source\icon\main.ico"

------

第18～26行定义了对话框DIALOG，该对话框最终的显示效果如图2-1所示。窗口定义中包含了窗口的显示样式、标题栏文字、窗口中包含的菜单IDM_MAIN，以及窗口字体格式。窗口工作区域中只包含了一个富文本框控件IDC_INFO（在第24行和第25行定义）。

第28～46行定义了菜单IDM_MAIN。它包含两个弹出式下拉菜单，分别命名为“文件”和“查看”，每个菜单中又各包含了多个菜单选项。

### 2.生成资源目标文件pe.res

接下来编译资源文件，生成资源目标文件（扩展名为res）。在命令提示符下输入以下命令（加粗部分）：

------

D:\masm32\source\chapter2＞rc -r pe.rc

------

如果执行编译时没有错误发生（如资源脚本中定义的相关文件不存在就会抛出错误提示），则命令执行后会在当前目录下生成资源目标文件pe.res。该资源目标文件最终要被链接程序嵌入到PE文件中，构成PE资源表所描述的数据的一部分。

## 2.1.3　通用程序框架的实现

资源目标文件生成以后，接下来的工作就是实现通用程序框架。主要分为三个阶段：

编写源程序pe.asm

编译生成目标文件pe.obj

链接生成可执行文件pe.exe

下面分别介绍各阶段的详细内容。

### 1.编写源程序pe.asm

首先，打开记事本，输入代码清单2-2所示的内容（去掉前面的行号）。

代码清单2-2　通用程序框架的源程序（chapter2\pe.asm）

------

```assembly
.386
.model flat,stdcall
option casemap:none

include    windows.inc
include    user32.inc
includelib user32.lib
include    kernel32.inc
includelib kernel32.lib
include    comdlg32.inc
includelib comdlg32.lib


ICO_MAIN equ 1000
DLG_MAIN equ 1000
IDC_INFO equ 1001
IDM_MAIN equ 2000
IDM_OPEN equ 2001
IDM_EXIT equ 2002
IDM_1    equ 4000
IDM_2    equ 4001
IDM_3    equ 4002

.data
hInstance   dd ?
hRichEdit   dd ?
hWinMain    dd ?
hWinEdit    dd ?
szFileName  db MAX_PATH dup(?)

.const
szDllEdit   db 'RichEd20.dll',0
szClassEdit db 'RichEdit20A',0
szFont      db '宋体',0


.code

;----------------
;初始化窗口程序
;----------------
_init proc
  local @stCf:CHARFORMAT
  
  invoke GetDlgItem,hWinMain,IDC_INFO
  mov hWinEdit,eax
  invoke LoadIcon,hInstance,ICO_MAIN
  invoke SendMessage,hWinMain,WM_SETICON,ICON_BIG,eax       ;为窗口设置图标
  invoke SendMessage,hWinEdit,EM_SETTEXTMODE,TM_PLAINTEXT,0 ;设置编辑控件
  invoke RtlZeroMemory,addr @stCf,sizeof @stCf
  mov @stCf.cbSize,sizeof @stCf
  mov @stCf.yHeight,9*20
  mov @stCf.dwMask,CFM_FACE or CFM_SIZE or CFM_BOLD
  invoke lstrcpy,addr @stCf.szFaceName,addr szFont
  invoke SendMessage,hWinEdit,EM_SETCHARFORMAT,0,addr @stCf
  invoke SendMessage,hWinEdit,EM_EXLIMITTEXT,0,-1
  ret
_init endp


;-------------------
; 窗口程序
;-------------------
_ProcDlgMain proc uses ebx edi esi hWnd,wMsg,wParam,lParam
  mov eax,wMsg
  .if eax==WM_CLOSE
    invoke EndDialog,hWnd,NULL
  .elseif eax==WM_INITDIALOG  ;初始化
    push hWnd
    pop hWinMain
    call _init
  .elseif eax==WM_COMMAND  ;菜单
    mov eax,wParam
    .if eax==IDM_EXIT       ;退出
      invoke EndDialog,hWnd,NULL 
    .elseif eax==IDM_OPEN   ;打开文件
    .elseif eax==IDM_1  
    .elseif eax==IDM_2
    .elseif eax==IDM_3
    .endif
  .else
    mov eax,FALSE
    ret
  .endif
  mov eax,TRUE
  ret
_ProcDlgMain endp

start:
  invoke LoadLibrary,offset szDllEdit
  mov hRichEdit,eax
  invoke GetModuleHandle,NULL
  mov hInstance,eax
  invoke DialogBoxParam,hInstance,\
         DLG_MAIN,NULL,offset _ProcDlgMain,NULL
  invoke FreeLibrary,hRichEdit
  invoke ExitProcess,NULL
  end start

```

------

代码清单2-2中的第98行通过调用DialogBoxParam函数创建了一个弹出式窗口作为整个程序的主窗口，并将内部函数_ProcDlgMain的地址当成该函数的参数之一传入该函数。函数_ProcDlgMain是弹出窗口的回调函数，如果要对发生在窗口中的消息进行捕获，则需要在此函数中设置对不同的消息进行响应的代码。由于本实例只是一个基本程序框架，所以回调函数只对菜单中的“退出”选项做了响应，如下所示：

------

```cpp
.if eax == WM_CLOSE
invoke EndDialog,hWnd,NULL
```

------

代码编写完成以后保存为pe.asm文件，然后编译生成目标文件。

### 2.编译生成目标文件pe.obj

在编写较大的程序时，通常会根据功能将源代码分别写到不同的文件里。有时为了分工合作，同一个项目中还会出现使用不同语言编写的源代码。这些源代码文件都需要在各自独立的环境中被编译成各自的目标文件。目标文件符合通用对象文件格式（COFF），该格式的定制主要是为了方便混合编程。生成目标文件的过程是处理源代码中可能会出现错误（如引入外部符号错误、源代码语法错误等）的过程，生成的目标文件最终会被链接程序拼接到最终的可执行文件中，当然，除了编译源代码生成的目标文件外，可执行文件还包含资源目标文件、外部引入的符号等信息。

在命令提示符下输入以下命令，编译源文件pe.asm：

------

D:\masm32\source\chapter2＞==ml -c -coff pe.asm==

------

如果没有错误，则会在当前目录下生成目标文件pe.obj。

接下来链接所有的目标文件（包括资源目标文件和源代码目标文件），生成最终的可执行文件。

### 3.链接生成可执行文件pe.exe

在命令提示符下输入以下命令：

------

D:\masm32\source\chapter2＞==link -subsystem:windows pe.obj pe.res==

------

上述命令指定了最终生成的EXE文件的运行平台为Windows，链接程序将根据pe.obj中的描述构造PE文件，并将相关资源内容附加到PE文件里，最终生成可执行的pe.exe。在命令提示符下输入"pe"，然后回车，即可看到最终的运行效果，如图2-1所示。

至此，一个基本的基于汇编语言的窗口程序就编写完成了。接下来的工作就是在此基础上进行扩展，开发三个基于Windows PE的小工具，首先来看字节码查看器PEDump的编写。

# 2.2　PEDump的实现

PEDump是PE文件字节码查看器，利用它可以查看和阅读指定PE文件的十六进制字节码，帮助我们更好地分析PE结构。

## 2.2.1　编程思路

编写PEDump的重点在于显示功能，首先来看一看最终可能的输出效果，如图2-2所示。

![image](https://github.com/YangLuchao/img_host/raw/master/20230725/image.2yxkcqi4myy0.webp)

图　2-2　PEDump的输出效果

如图所示，最终输出包含三列内容。

第一列是地址。地址的值是第（n×16+1）个字节在文件中的位置。

第二列是由空格分隔符分隔的16个字节的十六进制显示。

第三列是这16个字节对应的ASCⅡ码值。如果ASCⅡ码中无对应值，或者这些值是一些功能键，则以“.”代替。

> 注意　有的查看器（如FlexHex）还增加了Unicode字符一列，用来显示字节码中包含的Unicode字符。

编程时要考虑到最后一行可能会少于16个字节，这时候第二列和第三列不足的地方就可以使用空格补足。程序编写的流程如图2-3所示。

![image](https://github.com/YangLuchao/img_host/raw/master/20230725/image.40nkwvtbsfk0.webp)

图　2-3　编写PEDump程序的流程

#### 步骤1　打开PE文件。

需要说明的是，打开的PE文件会被映射为内存文件。因为内存文件中的内容是线性存放的，存取方便，速度也快，并且操作起来比在文件中使用指针定位要更容易些。

#### 步骤2　使用API函数GetFileSize得到该PE文件的大小。

步骤3～5　将第2步获取的值与16相除，商作为循环计数，余数则是字节码查看器最后一行的字节个数。在程序中构造一个循环，用来显示PE文件除最后一行外其他行的字节内容。

==为了更好地理解该开发过程，我们需要理解两个概念：内存映射文件与PE内存映像。==

内存映射文件是指将硬盘上的文件不做修改地装载到内存中。这样，文件中字节与字节之间就是顺序排列的了。在硬盘上，文件被分割成若干簇，这些簇不一定会按照文件内容顺序排列在一起，当我们访问磁盘上的文件时，需要计算机首先将不同位置的内容读取到内存。有了内存映射文件，访问就会变得更轻松和快捷，由于读取磁盘的操作集中到了一起执行，读写效率会提高很多。被一次性读取到内存的文件字节按线性排序，访问相对简单，速度也提升了不少。所以，许多大型的编辑软件在设计中经常会使用内存映射文件存取磁盘文件。

PE内存映像是指将PE文件按照一定的规则装载到内存中，装入后的整个文件头内容不会发生变化，但PE文件的某一部分如节的内容会按照字段中的对齐方式在内存中对齐，从而使得内存中的PE映像与装载前的PE文件不同。那么，为什么PE内存映像不能和一般的内存映射文件一样呢？答案很简单：PE文件是由操作系统装载进内存的，其目的是为了运行。为了配合操作系统的运行，方便调度，提高运行效率，PE映像必须按照一定的格式对齐，所以内存中的PE映像和原来硬盘上的文件是不同的，当然与内存映射文件也就不一样了。

## 2.2.2　PEDump编码

前面简单了解了程序的开发流程，接下来进入编码阶段。此处将会用到2.1节中的源程序文件pe.asm。

PEDump.asm在pe.asm的基础上增加了对菜单项IDM_OPEN的响应代码，如下所示：

------

```assembly
.elseif eax == IDM_OPEN ;打开文件
invoke _openFile
```

------

函数_openFile的实现如代码清单2-3所示。

代码清单2-3　PEDump主要函数_openFile实现（chapter2\pedump.asm）

------

```assembly
.386
.model flat,stdcall
option casemap:none

include    windows.inc
include    user32.inc
includelib user32.lib
include    kernel32.inc
includelib kernel32.lib
include    comdlg32.inc
includelib comdlg32.lib


ICO_MAIN equ 1000
DLG_MAIN equ 1000
IDC_INFO equ 1001
IDM_MAIN equ 2000
IDM_OPEN equ 2001
IDM_EXIT equ 2002
IDM_1    equ 4000
IDM_2    equ 4001
IDM_3    equ 4002

.data
hInstance   dd ?   ; 进程句柄
hRichEdit   dd ?
hWinMain    dd ?   ; 弹出窗口句柄
hWinEdit    dd ?   ; 富文本框句柄
totalSize   dd ?   ; 文件大小
lpMemory    dd ?   ; 内存映像文件在内存的起始位置
szFileName  db MAX_PATH dup(?)               ;要打开的文件路径及名称名

lpServicesBuffer         db 100 dup(0)   ;所有内容
bufDisplay               db 50 dup(0)      ;第三列ASCII码字符显示
szBuffer                 db 200 dup(0)       ;临时缓冲区
lpszFilterFmt4           db  '%08x  ',0
lpszManyBlanks           db  '  ',0
lpszBlank                db  ' ',0
lpszSplit                db  '-',0
lpszScanFmt              db  '%02x',0
lpszHexArr               db  '0123456789ABCDEF',0
lpszReturn               db  0dh,0ah,0
lpszDoubleReturn         db  0dh,0ah,0dh,0ah,0
lpszOut1                 db  '文件大小：%d',0
dwStop                   dd  0
.const
szDllEdit   db 'RichEd20.dll',0
szClassEdit db 'RichEdit20A',0
szFont      db '宋体',0
szExtPe     db 'PE File',0,'*.exe;*.dll;*.scr;*.fon;*.drv',0
            db 'All Files(*.*)',0,'*.*',0,0
szErr       db '文件格式错误!',0
szErrFormat db '操作文件时出现错误!',0


.code

;----------------
;初始化窗口程序
;----------------
_init proc
  local @stCf:CHARFORMAT
  
  invoke GetDlgItem,hWinMain,IDC_INFO
  mov hWinEdit,eax
  invoke LoadIcon,hInstance,ICO_MAIN
  invoke SendMessage,hWinMain,WM_SETICON,ICON_BIG,eax       ;为窗口设置图标
  invoke SendMessage,hWinEdit,EM_SETTEXTMODE,TM_PLAINTEXT,0 ;设置编辑控件
  invoke RtlZeroMemory,addr @stCf,sizeof @stCf
  mov @stCf.cbSize,sizeof @stCf
  mov @stCf.yHeight,14*1440/96
  mov @stCf.dwMask,CFM_FACE or CFM_SIZE or CFM_BOLD
  invoke lstrcpy,addr @stCf.szFaceName,addr szFont
  invoke SendMessage,hWinEdit,EM_SETCHARFORMAT,0,addr @stCf
  invoke SendMessage,hWinEdit,EM_EXLIMITTEXT,0,-1
  ret
_init endp

;------------------
; 错误Handler
;------------------
_Handler proc _lpExceptionRecord,_lpSEH,\
              _lpContext,_lpDispathcerContext

  pushad
  mov esi,_lpExceptionRecord
  mov edi,_lpContext
  assume esi:ptr EXCEPTION_RECORD,edi:ptr CONTEXT
  mov eax,_lpSEH
  push [eax+0ch]
  pop [edi].regEbp
  push [eax+8]
  pop [edi].regEip
  push eax
  pop [edi].regEsp
  assume esi:nothing,edi:nothing
  popad
  mov eax,ExceptionContinueExecution
  ret
_Handler endp

;---------------------
; 往文本框中追加文本
;---------------------
_appendInfo proc _lpsz
  local @stCR:CHARRANGE

  pushad
  invoke GetWindowTextLength,hWinEdit
  mov @stCR.cpMin,eax  ;将插入点移动到最后
  mov @stCR.cpMax,eax
  invoke SendMessage,hWinEdit,EM_EXSETSEL,0,addr @stCR
  invoke SendMessage,hWinEdit,EM_REPLACESEL,FALSE,_lpsz
  popad
  ret
_appendInfo endp


;--------------------
; 打开PE文件并处理
;--------------------
_openFile proc
  local @stOF:OPENFILENAME
  local @hFile,@hMapFile
  local @bufTemp1   ; 十六进制字节码
  local @bufTemp2   ; 第一列
  local @dwCount    ; 计数，逢16则重新计
  local @dwCount1   ; 地址顺号
  local @dwBlanks   ; 最后一行空格数

  invoke RtlZeroMemory,addr @stOF,sizeof @stOF
  mov @stOF.lStructSize,sizeof @stOF
  push hWinMain
  pop @stOF.hwndOwner
  mov @stOF.lpstrFilter,offset szExtPe
  mov @stOF.lpstrFile,offset szFileName
  mov @stOF.nMaxFile,MAX_PATH
  mov @stOF.Flags,OFN_PATHMUSTEXIST or OFN_FILEMUSTEXIST
  invoke GetOpenFileName,addr @stOF  ;让用户选择打开的文件
  .if !eax
    jmp @F
  .endif
  invoke CreateFile,addr szFileName,GENERIC_READ,\
         FILE_SHARE_READ or FILE_SHARE_WRITE,NULL,\
         OPEN_EXISTING,FILE_ATTRIBUTE_ARCHIVE,NULL
  .if eax!=INVALID_HANDLE_VALUE
    mov @hFile,eax
    invoke GetFileSize,eax,NULL     ;获取文件大小
    mov totalSize,eax

    .if eax
      invoke CreateFileMapping,@hFile,\  ;内存映射文件
             NULL,PAGE_READONLY,0,0,NULL
      .if eax
        mov @hMapFile,eax
        invoke MapViewOfFile,eax,\
               FILE_MAP_READ,0,0,0
        .if eax
          mov lpMemory,eax              ;获得文件在内存的映象起始位置
          assume fs:nothing
          push ebp
          push offset _ErrFormat
          push offset _Handler
          push fs:[0]
          mov fs:[0],esp

          ;开始处理文件

          ;缓冲区初始化
          invoke RtlZeroMemory,addr @bufTemp1,10
          invoke RtlZeroMemory,addr @bufTemp2,20
          invoke RtlZeroMemory,addr lpServicesBuffer,100
          invoke RtlZeroMemory,addr bufDisplay,50

          mov @dwCount,1
          mov esi,lpMemory
          mov edi,offset bufDisplay
 
          ; 将第一列写入lpServicesBuffer
          mov @dwCount1,0
          invoke wsprintf,addr @bufTemp2,addr lpszFilterFmt4,@dwCount1
          invoke lstrcat,addr lpServicesBuffer,addr @bufTemp2

          ;求最后一行的空格数（16－长度％16）*3
          xor edx,edx
          mov eax,totalSize
          mov ecx,16
          div ecx
          mov eax,16
          sub eax,edx
          xor edx,edx
          mov ecx,3
          mul ecx
          mov @dwBlanks,eax

          ;invoke wsprintf,addr szBuffer,addr lpszOut1,totalSize
          ;invoke MessageBox,NULL,addr szBuffer,NULL,MB_OK

          .while TRUE
             .if totalSize==0  ;最后一行
                ;填充空格
                 .while TRUE
                     .break .if @dwBlanks==0
                     invoke lstrcat,addr lpServicesBuffer,addr lpszBlank
                     dec @dwBlanks
                 .endw
                 ;第二列与第三列中间的空格
                 invoke lstrcat,addr lpServicesBuffer,addr lpszManyBlanks  
                 ;第三列内容
                 invoke lstrcat,addr lpServicesBuffer,addr bufDisplay
                 ;回车换行符号
                 invoke lstrcat,addr lpServicesBuffer,addr lpszReturn
                 .break
             .endif
             ;将al翻译成可以显示的ascii码字符,注意不能破坏al的值
             mov al,byte ptr [esi]
             .if al>20h && al<7eh
                mov ah,al
             .else        ;如果不是ASCII码值，则显示“.”
                mov ah,2Eh
             .endif
             ;写入第三列的值
             mov byte ptr [edi],ah

            ;win2k不支持al字节级别，经常导致程序无故结束，
            ;因此用以下方法替代
            ;invoke wsprintf,addr @bufTemp1,addr lpszFilterFmt3,al
                         
             mov bl,al
             xor edx,edx
             xor eax,eax
             mov al,bl
             mov cx,16
             div cx   ;结果高位在al中，余数在dl中

             ;组合字节的十六进制字符串到@bufTemp1中，类似于：“7F \0”
             push edi
             xor bx,bx
             mov bl,al
             movzx edi,bx
             mov bl,byte ptr lpszHexArr[edi]
             mov byte ptr @bufTemp1[0],bl

             xor bx,bx
             mov bl,dl
             movzx edi,bx
             mov bl,byte ptr lpszHexArr[edi]
             mov byte ptr @bufTemp1[1],bl
             mov bl,20h
             mov byte ptr @bufTemp1[2],bl
             mov bl,0
             mov byte ptr @bufTemp1[3],bl
             pop edi

             ; 将第二列写入lpServicesBuffer
             invoke lstrcat,addr lpServicesBuffer,addr @bufTemp1

             .if @dwCount==16   ;已到16个字节，
                ;第二列与第三列中间的空格
                invoke lstrcat,addr lpServicesBuffer,addr lpszManyBlanks
                ;显示第三列字符 
                invoke lstrcat,addr lpServicesBuffer,addr bufDisplay        
                ;回车换行
                invoke lstrcat,addr lpServicesBuffer,addr lpszReturn 

                ;写入内容
                invoke _appendInfo,addr lpServicesBuffer
                invoke RtlZeroMemory,addr lpServicesBuffer,100           

                .break .if dwStop==1

                ;显示下一行的地址
                inc @dwCount1
                invoke wsprintf,addr @bufTemp2,addr lpszFilterFmt4,\
                                                            @dwCount1
                invoke lstrcat,addr lpServicesBuffer,addr @bufTemp2
                dec @dwCount1

                mov @dwCount,0
                invoke RtlZeroMemory,addr bufDisplay,50
                mov edi,offset bufDisplay
                ;为了能和后面的inc edi配合使edi正确定位到bufDisplay处
                dec edi 
             .endif

             dec totalSize
             inc @dwCount
             inc esi
             inc edi
             inc @dwCount1
          .endw

          ;添加最后一行
          invoke _appendInfo,addr lpServicesBuffer

          
          ;处理文件结束

          jmp _ErrorExit
 
_ErrFormat:
          invoke MessageBox,hWinMain,offset szErrFormat,NULL,MB_OK
_ErrorExit:
          pop fs:[0]
          add esp,0ch
          invoke UnmapViewOfFile,lpMemory
        .endif
        invoke CloseHandle,@hMapFile
      .endif
      invoke CloseHandle,@hFile
    .endif
  .endif
@@:        
  ret
_openFile endp
;-------------------
; 窗口程序
;-------------------
_ProcDlgMain proc uses ebx edi esi hWnd,wMsg,wParam,lParam
  local @sClient

  mov eax,wMsg
  .if eax==WM_CLOSE
    invoke EndDialog,hWnd,NULL
  .elseif eax==WM_INITDIALOG  ;初始化
    push hWnd
    pop hWinMain
    call _init
  .elseif eax==WM_COMMAND  ;菜单
    mov eax,wParam
    .if eax==IDM_EXIT       ;退出
      invoke EndDialog,hWnd,NULL 
    .elseif eax==IDM_OPEN   ;打开文件
      mov dwStop,0
      invoke CreateThread,NULL,0,addr _openFile,addr @sClient,0,NULL
      ;invoke _openFile
    .elseif eax==IDM_1 
      mov dwStop,1 
    .elseif eax==IDM_2
    .elseif eax==IDM_3
    .endif
  .else
    mov eax,FALSE
    ret
  .endif
  mov eax,TRUE
  ret
_ProcDlgMain endp

start:
  invoke LoadLibrary,offset szDllEdit
  mov hRichEdit,eax
  invoke GetModuleHandle,NULL
  mov hInstance,eax
  invoke DialogBoxParam,hInstance,\
         DLG_MAIN,NULL,offset _ProcDlgMain,NULL
  invoke FreeLibrary,hRichEdit
  invoke ExitProcess,NULL
  end start
```

------

子程序_openFile首先调用GetOpenFileName，显示一个文件选择对话框，让用户选择要打开的PE文件；然后，获取指定文件的大小，并利用这个值通过函数CreateFileMapping在内存中建立该文件的映像；==全局变量lpMemory指向了内存映像的起始地址==。有了这个地址以后，对文件进行各种操作就简单多了。

第50～177行是对内存映像文件的处理过程。这个过程如果太复杂，可以继续使用子程序；如果不是很复杂，则可以直接在此编写处理代码。十六进制字节码查看器的主要代码如下：

------

```cpp
// 全部代码如上，当前略
```

------

完整的源代码请查看随书文件chapter2\pedump.asm。在该部分代码中，每取一个字节，都会将其ASCⅡ码的值写入bufDisplay。如果字节的值在20h和7eh之间，则显示相应的ASCⅡ码，否则显示“.”。每计数16个字节，就会重新初始化bufDisplay。

每取一个字节，都会将该字节的十六进制字符表示形式加上后面的空格作为一个完整单位，附加到lpServicesBuffer。每计数16个字节，就会将lpServicesBuffer中存放的完整的一行内容写入到富文本框中。

## 2.2.3　PEDump代码中的数据结构

为了帮助大家更好地阅读PEDump的实现代码，在此分别列出本程序中用到的全局变量和局部变量。

### （1）程序中用到的全局变量

![image](https://github.com/YangLuchao/img_host/raw/master/20230725/image.792ssga0lb80.webp)

### （2）程序中用到的局部变量

------

```assembly
local @bufTemp1 ;十六进制字节码
local @bufTemp2 ;第一列缓冲区
local @dwCount ;计数，逢16则重新计
local @dwCount1 ;地址序号
local @dwBlanks ;最后一行的空格数
```

------

结合代码清单2-3，对生成PE字节码的过程解释如下：

第一列内容的生成（第63～64行）程序通过组合字符串函数wsprintf构造第一列内容，生成的字符串被存储在@bufTemp2中，使用lstrcat函数将该内容加到lpServicesBuffer中。如下所示：

------

```assembly
63 invoke wsprintf,addr @bufTemp2,addr lpszFilterFmt4,@dwCount1
64 invoke lstrcat,addr lpServicesBuffer,addr @bufTemp2
```

------

第二列内容的生成（第111～135行）通过循环构造第二列字节码的内容，把每一个字节的字节码字符串写入缓冲区@bufTemp1，然后使用lstrcat函数将这个字节的内容加到lpServicesBuffer中。

> 注意　@bufTemp1中存放的并不是第二列所有的内容，而是一个字节的内容。内容包括字节十六进制表示+空格+结尾的“\0”字符。假设该字节为80h，则存储在@bufTemp1中的内容为“80\0”。

第三列内容的生成（第97～105行）每取一个字节，就会判断该字节的值是否介于20h和7eh之间，如果是则将相应的ASCⅡ码写入变量bufDisplay，否则写入“.”到变量bufDisplay。

每行的生成　lpServicesBuffer代表每行的缓冲区，每读取16个字节，就会将该缓冲区中的内容写入到新文件中。如果是最后一行，则在循环中单独处理（见代码清单2-3的第82～96行）。

## 2.2.4　运行PEDump

打开命令提示符窗口，在D:\masm32\source\chapter2目录下执行如下命令：

```bash
rc -r pedump.rc（编译资源脚本文件）
ml -c -coff pedump.asm（编译PEDump.asm）
link -subsystem:windows pedump.res pedump.obj（链接生成可执行程序）
```

运行PEDump.exe，最终效果如图2-4所示。

![image](https://github.com/YangLuchao/img_host/raw/master/20230725/image.up9wk300fww.webp)

图　2-4　PEDump的运行效果

在测试多个打开的PE文件后你会发现程序存在一个问题，当程序在打开比较大的PE文件显示字节码时界面会发生假死，这是由于程序主线程的循环造成了系统消息堵塞，从而无法完成界面更新所致。要解决这个问题，应该把事件响应代码_openFile单独放到一个开启的线程中执行，方法如下。

将以下菜单响应代码：

------

```cpp
invoke _openFile
```

------

更改为：

------

```cpp
invoke CreateThread,NULL,0,addr _openFile,addr @sClient,0,NULL
```

------

为了能随时终止滚动显示，可以在主程序中增加一个标志字节，然后在查看菜单中增加一个“停止dump...”菜单选项，该选项的响应代码为：

------

```cpp
.elseif eax == IDM_1 ;停止dump
mov dwStop,1
```

------

线程函数_openFile的循环中也有对应的检测dwStop的代码，如下所示：

------

```cpp
.while TRUE
......
.break.if dwStop == 1
......
.endw
```

------

经过如上设计，在程序显示字节码的过程中，任意拖曳运行窗口都不会出现界面假死的现象。同时，用户也可以通过菜单项“查看”|“停止Dump...”随时终止字节码的显示，退出循环。

至此，PE文件字节码查看器的编写完成。可以看出，在通用程序框架的基础上实施再编码，不仅为开发者节省了很多的时间，提高了开发效率，同时也有利于开发者把主要精力集中到关键代码的编写上。

# 2.3　PEComp的实现

PEComp是PE文件比较器。功能是按照PE文件格式的数据结构按字段对两个指定的PE文件进行比对，以获取两个PE文件结构中的不相同的信息。==在实际应用中，我们可以通过对比病毒感染前后PE文件在相关字段上发生的变化来判断病毒的感染方式，从而确定清理病毒的方法。==

## 2.3.1　编程思路

PEComp的功能是在通用框架pe.asm的基础上，实现两个PE文件的对比，并通过图形界面将不同之处形象地表示出来。编码的大致思路如下：

1. 步骤1　打开要比较的两个文件，分别进行文件的内存映射，获取内存起始地址。
2. 步骤2　线性搜索，根据文件头部内容确定该文件是否为PE文件，不是则退出。
3. 步骤3　将esi指向要操作的第一个文件的相关字段处，将edi指向第二个要操作的文件的相同字段处，同时获取该位置的指定个数的字节到内存，比较并显示，如果不同，则显示时使用红色背景以示区别。

下面开始具体的设计过程，首先根据构思的最终显示效果定义资源文件。

## 2.3.2　定义资源文件

将2.1.2节中的资源文件pe.rc复制到pecomp.rc，并在pecomp.rc文件中增加一个对话框，该对话框中包括用户选择的参与对比的两个PE文件文本框ID_TEXT1和ID_TEXT2、两个浏览按钮、一个显示结果用的表格IDC_MODULETABLE和一个执行按钮，增加的对话框脚本定义如下所示：

------

```assembly
#include <resource.h>

#define ICO_MAIN  1000
#define DLG_MAIN  1000
#define IDC_INFO  1001
#define IDM_MAIN  2000
#define IDM_OPEN  2001
#define IDM_EXIT  2002


#define IDM_1    4000
#define IDM_2    4001
#define IDM_3    4002
#define IDM_4    4003

#define RESULT_MODULE 5000
#define ID_TEXT1  5001
#define ID_TEXT2  5002
#define IDC_MODULETABLE 5003
#define IDC_OK 5004
#define ID_STATIC 5005
#define ID_STATIC1 5006
#define IDC_BROWSE1 5007
#define IDC_BROWSE2 5008
#define IDC_THESAME 5009



ICO_MAIN  ICON  "main.ico"

DLG_MAIN DIALOG 50,50,544,399
STYLE DS_MODALFRAME | WS_POPUP | WS_VISIBLE | WS_CAPTION | WS_SYSMENU
CAPTION "PEComp by qixiaorui"
MENU IDM_MAIN
FONT 9,"宋体"
BEGIN
   CONTROL "",IDC_INFO,"RichEdit20A",196 | ES_WANTRETURN | WS_CHILD | ES_READONLY
               | WS_VISIBLE |WS_BORDER | WS_VSCROLL | WS_TABSTOP,0,0,540,396
END

RESULT_MODULE DIALOG 76,10,630,480
STYLE DS_MODALFRAME | WS_POPUP |WS_VISIBLE | WS_CAPTION |WS_SYSMENU
CAPTION "PE文件对比结果"
FONT 9,"宋体"
BEGIN
  LTEXT "您选定的第一个文件为：",ID_STATIC,10,13,200,15
  EDITTEXT ID_TEXT1,130,13,440,15
  PUSHBUTTON "浏览...",IDC_BROWSE1,570,13,50,14

  LTEXT "您选定的第二个文件为：",ID_STATIC1,10,35,200,15
  EDITTEXT ID_TEXT2,130,35,440,15
  PUSHBUTTON "浏览...",IDC_BROWSE2,570,35,50,14

  CONTROL "", IDC_MODULETABLE, "SysListView32",13 | WS_CHILD | WS_VISIBLE | WS_BORDER | WS_TABSTOP, 10,60,610,390  

  AUTOCHECKBOX "只显示不同的值" IDC_THESAME,10,460,100,14
  PUSHBUTTON "执行...(&R)",IDC_OK,570,460,50,14
END

IDM_MAIN menu discardable
BEGIN
  POPUP "文件(&F)"
  BEGIN
    menuitem "打开PE对比对话框",IDM_OPEN
    menuitem "---",IDM_1
    menuitem "---",IDM_2
    menuitem "---",IDM_3 CHECKED
    menuitem separator
    menuitem "退出(&x)",IDM_EXIT
  END
  POPUP "编辑(&E)"
  BEGIN
    menuitem separator
  END

  POPUP "格式(&O)"
  BEGIN
    menuitem separator
  END

  POPUP "查看(&V)"
  BEGIN
    menuitem "源文件",IDM_1
    menuitem "窗口透明度",IDM_2
    menuitem separator
    menuitem "大小",IDM_3
    menuitem "宽度",IDM_4
  END

  POPUP "帮助(&H)"
  BEGIN
    menuitem separator
  END

END
```

------

## 2.3.3　PEComp编码

复制pe.asm到PEComp.asm，并从代码中的窗口回调函数的菜单项响应部分开始编码，增加内容有以下4个部分。

#### 1.菜单项响应代码

在主窗口的回调函数中，定义对鼠标点击菜单项“文件”|“打开”所引发的消息处理程序，添加如下代码：

------

```assembly
    .elseif eax==IDM_OPEN   ;打开PE对比对话框
         invoke DialogBoxParam,hInstance,RESULT_MODULE,hWnd,\
               offset _resultProcMain,0
         invoke InvalidateRect,hWnd,NULL,TRUE
         invoke UpdateWindow,hWnd
```

------

当用户选择了“打开”菜单选项时，会弹出资源文件里定义的对话框RESULT_MODULE。通过定义该对话框的回调函数，可以处理对话框控件发出的消息。该对话框的回调函数是_resultProcMain，其代码如代码清单2-4所示。

代码清单2-4　PE文件比较器的窗口回调函数_resultProcMain实现（chapter2\pecomp.asm）

------

```assembly
;-----------------------
; 弹出PE对比窗口回调函数
;-----------------------
_resultProcMain   proc  uses ebx edi esi hProcessModuleDlg:HWND,wMsg,wParam,lParam
          mov eax,wMsg

          .if eax==WM_CLOSE
             invoke EndDialog,hProcessModuleDlg,NULL
          .elseif eax==WM_INITDIALOG
             invoke GetDlgItem,hProcessModuleDlg,IDC_MODULETABLE
             mov hProcessModuleTable,eax
             invoke GetDlgItem,hProcessModuleDlg,ID_TEXT1
             mov hText1,eax
             invoke GetDlgItem,hProcessModuleDlg,ID_TEXT2
             mov hText2,eax
             
             ;定义表格外观
             invoke SendMessage,hProcessModuleTable,LVM_SETEXTENDEDLISTVIEWSTYLE,\
                    0,LVS_EX_GRIDLINES or LVS_EX_FULLROWSELECT
             invoke ShowWindow,hProcessModuleTable,SW_SHOW
             ;清空表格内容
             invoke _clearResultView

          .elseif eax==WM_NOTIFY
            mov eax,lParam
            mov ebx,lParam
            ;更改各控件状态
            mov eax,[eax+NMHDR.hwndFrom]
            .if eax==hProcessModuleTable
                mov ebx,lParam
                .if [ebx+NMHDR.code]==NM_CUSTOMDRAW  ;绘画时
                  mov ebx,lParam
                  assume ebx:ptr NMLVCUSTOMDRAW  
                  .if [ebx].nmcd.dwDrawStage==CDDS_PREPAINT
                     invoke SetWindowLong,hProcessModuleDlg,DWL_MSGRESULT,\
                                                               CDRF_NOTIFYITEMDRAW
                     mov eax,TRUE
                  .elseif [ebx].nmcd.dwDrawStage==CDDS_ITEMPREPAINT

                     ;当每一单元格内容预画时，判断
                     ;两列的值是否一致
                     invoke _GetListViewItem,hProcessModuleTable,\
                                         [ebx].nmcd.dwItemSpec,1,addr bufTemp1
                     invoke _GetListViewItem,hProcessModuleTable,\
                                         [ebx].nmcd.dwItemSpec,2,addr bufTemp2
                     invoke lstrlen,addr bufTemp1
                     invoke _MemCmp,addr bufTemp1,addr bufTemp2,eax

                     ;如果一致，则将文本的背景色设置为浅红色，否则黑色
                     .if eax==1
                        mov [ebx].clrTextBk,0a0a0ffh
                     .else
                        mov [ebx].clrTextBk,0ffffffh
                     .endif
                     invoke SetWindowLong,hProcessModuleDlg,DWL_MSGRESULT,\
                                                                CDRF_DODEFAULT
                     mov eax,TRUE
                   .endif
                .elseif [ebx+NMHDR.code]==NM_CLICK
                    assume ebx:ptr NMLISTVIEW
                .endif
            .endif
          .elseif eax==WM_COMMAND
             mov eax,wParam
             .if ax==IDC_OK  ;刷新
                invoke _openFile
             .elseif ax==IDC_BROWSE1
                invoke _OpenFile1    ;用户选择第一个文件
             .elseif ax==IDC_BROWSE2
                invoke _OpenFile2    ;用户选择第二个文件
             .endif
         .else
             mov eax,FALSE
             ret
         .endif
         mov eax,TRUE
         ret
_resultProcMain    endp

```

------

PE文件比较器窗口回调函数中最主要的代码集中在第31～58行。由窗口回调函数注册的监听器一旦发现由表格控件引发了绘画消息，则判断该绘画是否为表格项重画（第38行）。如果是，则获取要重画的项目当前所在行的第1列和第2列的值。这两个值来自于两个不同PE文件的同一个字段，通过判断二者是否相等来决定重画时使用的背景色。如果不相等，则将重画时的背景色设置为0a0a0ffh（浅红色），否则设置为0ffffffh（黑色）。

如上所示，当用户选择了两个要参与对比的PE文件以后，点击执行对比按钮，系统会调用函数_openFile。

#### 2._openFile函数

_openFile函数的功能是把两个PE文件数据结构中的相关字段的值取出，分别放到表格的第1列和第2列，判断两个值是否相等的代码在回调函数_resultProcMain中已经给出。

与前面的思路一样，程序还是使用了内存映射函数来操作参与对比的两个PE文件，所不同的是这里需要定义两个指针。一个指针指向第一个文件的内存映射函数的起始位置，另一个指针指向第二个文件的内存映射函数的起始位置。假设该工作已经完成，接下来就是把两个PE文件按照第3章里描述的所有字段的值取出来显示到表格中，完成该功能的代码如代码清单2-5所示。

代码清单2-5　_openFile函数实现的部分代码

------

```assembly
;到此为止，两个内存文件的指针已经获取到了。
  ;@lpMemory和@lpMemory1分别指向两个文件头
  ;下面是从这个文件头开始，找出各数据结构的字段值，进行比较。

  ;调整ESI,EDI指向DOS头
  mov esi,@lpMemory
  assume esi:ptr IMAGE_DOS_HEADER
  mov edi,@lpMemory1
  assume edi:ptr IMAGE_DOS_HEADER
  invoke _Header1

  ;调整ESI,EDI指针指向PE文件头
  add esi,[esi].e_lfanew    
  assume esi:ptr IMAGE_NT_HEADERS
  add edi,[edi].e_lfanew    
  assume edi:ptr IMAGE_NT_HEADERS
  invoke _Header2

  movzx ecx,word ptr [esi+6]
  movzx eax,word ptr [edi+6]

  .if eax>ecx
     mov ecx,eax
  .endif

  ;调整ESI,EDI指针指向节表
  add esi,sizeof IMAGE_NT_HEADERS
  add edi,sizeof IMAGE_NT_HEADERS
  mov eax,1
  .repeat
    invoke _Header3
    dec ecx
    inc eax
    .break .if ecx==0
    add esi,sizeof IMAGE_SECTION_HEADER
    add edi,sizeof IMAGE_SECTION_HEADER
  .until FALSE
```

------

由于编码比较长，这里只以结构IMAGE_DOS_HEADER中的字段e_lpanew为例介绍程序设计的流程：将esi和edi分别赋值到两个PE文件的IMAGE_DOS_HEADER后，调用函数_Header1，处理数据结构DOS头的相关字段（第5～10行）。

#### 3._Header1函数

_Header1函数完成了DOS头部分的字段比较，此部分的详细代码如代码清单2-6所示。

代码清单2-6　DOS头部分的字段比较函数_Header1（chapter2\pecomp.asm）

------

```assembly

;-----------------------
; IMAGE_DOS_HEADER头信息
;-----------------------
_Header1 proc 
  pushad

  invoke _addLine,addr szRec1,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec2,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec3,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec4,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec5,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec6,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec7,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec8,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec9,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec10,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec11,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec12,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec13,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec14,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec15,esi,edi,8
  add esi,8
  add edi,8
  invoke _addLine,addr szRec16,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec17,esi,edi,2
  add esi,2
  add edi,2
  invoke _addLine,addr szRec18,esi,edi,20
  add esi,20
  add edi,20
  invoke _addLine,addr szRec19,esi,edi,4
  popad
  ret
_Header1 endp
```

------

DOS头结构中字段e_lpanew的处理在第59～61行。首先，将esi和edi指向该字段所在内存位置；然后，调用_addLine函数将两个PE文件对应字段的值加入到表格中。

------

invoke _addLine,para1,para2,para3,para4

------

> _addLine的参数描述如下：
>
> para1：字段名称字符串所在地址。
>
> para2：PE文件1该字段值所在内存地址。
>
> para3：PE文件2该字段值所在内存地址。
>
> para4：该字段的长度（即字节数）。

#### 4._addLine函数

该函数完成了在表格中增加一行的操作，具体定义如代码清单2-7所示。

代码清单2-7　在表格中增加一行的函数_addLine（chapter2\pecomp.asm）

------

```assembly
;--------------------------------------------
; 在表格中增加一行
; _lpSZ为第一行要显示的字段名
; _lpSP1为第一个文件该字段的位置
; _lpSP2为第二个文件该字段的位置
; _Size为该字段的字节长度
;--------------------------------------------
_addLine proc _lpSZ,_lpSP1,_lpSP2,_Size
  pushad

  invoke _ListViewSetItem,hProcessModuleTable,dwCount,-1,\
               _lpSZ             ;在表格中新增加一行
  mov dwCount,eax

  xor ebx,ebx
  invoke _ListViewSetItem,hProcessModuleTable,dwCount,ebx,\
         _lpSZ                   ;显示字段名
  
  invoke RtlZeroMemory,addr szBuffer,50
  invoke MemCopy,_lpSP1,addr bufTemp2,_Size
  invoke _Byte2Hex,_Size

  ;将指定字段按照十六进制显示，格式：一个字节+一个空格
  invoke lstrcat,addr szBuffer,addr bufTemp1
  inc ebx
  invoke _ListViewSetItem,hProcessModuleTable,dwCount,ebx,\
                   addr szBuffer ;第一个文件中的值

  invoke RtlZeroMemory,addr szBuffer,50
  invoke MemCopy,_lpSP2,addr bufTemp2,_Size
  invoke _Byte2Hex,_Size
  invoke lstrcat,addr szBuffer,addr bufTemp1
  inc ebx
  invoke _ListViewSetItem,hProcessModuleTable,dwCount,ebx,\
                   addr szBuffer ;第二个文件中的值

  popad
  ret
_addLine  endp

```

------

显示字段值的同时，会在消息处理函数中调用字节比对函数_MemCmp以确定值是否相同，如果不相同则将表格行的背景色设置为红色以示区别。其他字段的处理方式与字段IMAGE_DOS_HEADER.e_lpanew的处理方式类似，不再一一陈述。

## 2.3.4　运行PEComp

编译资源文件PEComp.rc，编译链接PEComp.asm生成最终的PEComp.exe程序；将随书文件中目录chapter2下的两个测试用文件peinfoNor.bin和peinfoVir.bin重命名，其扩展名都改为exe，然后运行PEComp.exe程序。

依次选择菜单“文件”→“打开”，弹出PE对比对话框窗口，在对话框中选择并打开刚才重命名的两个EXE文件，然后进行对比，运行效果如图2-5所示。

![image](https://github.com/YangLuchao/img_host/raw/master/20230725/image.17nej7uq0jmk.webp)

图　2-5　PEComp运行效果

> 注意　请不要运行以上两个文件，因为peinfoVir.exe是个病毒文件。测试完以后请将扩展名改回原来的"bin"。

通过对比可以看出，在两个PE文件的文件头部结构中，字段不相同的部分来自最后一个节的描述。可以初步断定，该病毒程序是通过修改正常程序的最后一个节的相关字段的值来实现病毒代码携带的。

# 2.4　PEInfo的实现

PEInfo是PE文件结构查看器，它将PE中的字节码以形象的描述语言显示出来，塑造一个整体的PE形象。通过编写PEInfo，可以锻炼我们使用数据结构定位特定PE信息的能力。

## 2.4.1　编程思路

这个小工具开发起来也不难，只是过程复杂了一些而已，其编程思路如下：

#### 步骤1　打开文件，判断是否为PE文件。

判断方法非常简单，首先查看IMAGE_DOS_HEADER.e_magic字段，然后查看IMAGE_NT_HEADER.Signature字段；如果符合PE文件定义，则视为合法PE文件。事实上，操作系统在装载PE文件时，对PE文件的检测远比此方法复杂得多。

#### 步骤2　将指针定位到相关数据结构，获取字段内容并以更人性化的方式显示相关内容。

> 提示　由于我们还没有正式开始学习PE文件格式，而PEInfo编程中涉及PE头部的大量数据结构，所以该部分代码的阅读最好等学习完第3章以后再进行。

## 2.4.2　PEInfo编码

编写PEInfo不需要额外的资源文件，复制一份pe.rc到PEInfo.rc即可，源代码依然来自pe.asm。与pe.asm不同的是，我们需要在PEInfo.asm的窗口回调函数中，为菜单项“文件”|“打开”的消息响应代码加入调用_openFile函数的代码。如下所示：

------

```assembly
.elseif eax == IDM_OPEN ;打开文件
call _openFile
```

------

下面来看\_openFile函数和\_getMainInfo函数。

#### 1._openFile函数

_openFile函数完成了显示PE结构的所有功能，该部分代码如代码清单2-8所示。

代码清单2-8　_openFile函数实现（chapter2\peinfo.asm）

------

```assembly
;--------------------
; 打开PE文件并处理
;--------------------
_openFile proc
  local @stOF:OPENFILENAME
  local @hFile,@dwFileSize,@hMapFile,@lpMemory

  invoke RtlZeroMemory,addr @stOF,sizeof @stOF
  mov @stOF.lStructSize,sizeof @stOF
  push hWinMain
  pop @stOF.hwndOwner
  mov @stOF.lpstrFilter,offset szExtPe
  mov @stOF.lpstrFile,offset szFileName
  mov @stOF.nMaxFile,MAX_PATH
  mov @stOF.Flags,OFN_PATHMUSTEXIST or OFN_FILEMUSTEXIST
  invoke GetOpenFileName,addr @stOF  ;让用户选择打开的文件
  .if !eax
    jmp @F
  .endif
  invoke CreateFile,addr szFileName,GENERIC_READ,\
         FILE_SHARE_READ or FILE_SHARE_WRITE,NULL,\
         OPEN_EXISTING,FILE_ATTRIBUTE_ARCHIVE,NULL
  .if eax!=INVALID_HANDLE_VALUE
    mov @hFile,eax
    invoke GetFileSize,eax,NULL
    mov @dwFileSize,eax
    .if eax
      invoke CreateFileMapping,@hFile,\  ;内存映射文件
             NULL,PAGE_READONLY,0,0,NULL
      .if eax
        mov @hMapFile,eax
        invoke MapViewOfFile,eax,\
               FILE_MAP_READ,0,0,0
        .if eax
          ;获得文件在内存的映象起始位置
          mov @lpMemory,eax
          assume fs:nothing
          push ebp
          push offset _ErrFormat
          push offset _Handler
          push fs:[0]
          mov fs:[0],esp

          ;检测PE文件是否有效
          mov esi,@lpMemory
          assume esi:ptr IMAGE_DOS_HEADER

          ;判断是否有MZ字样
          .if [esi].e_magic!=IMAGE_DOS_SIGNATURE
            jmp _ErrFormat
          .endif

          ;调整ESI指针指向PE文件头
          add esi,[esi].e_lfanew
          assume esi:ptr IMAGE_NT_HEADERS
          ;判断是否有PE字样
          .if [esi].Signature!=IMAGE_NT_SIGNATURE
            jmp _ErrFormat
          .endif

          ;到此为止，该文件的验证已经完成。为PE结构文件
          ;接下来分析分件映射到内存中的数据，并显示主要参数
          invoke _getMainInfo,@lpMemory,esi,@dwFileSize
          ;显示导入表
          invoke _getImportInfo,@lpMemory,esi,@dwFileSize
          ;显示导出表
          invoke _getExportInfo,@lpMemory,esi,@dwFileSize
          ;显示重定位信息
          invoke _getRelocInfo,@lpMemory,esi,@dwFileSize


          jmp _ErrorExit
 
_ErrFormat:
          invoke MessageBox,hWinMain,offset szErrFormat,\
                                                 NULL,MB_OK
_ErrorExit:
          pop fs:[0]
          add esp,0ch
          invoke UnmapViewOfFile,@lpMemory
        .endif
        invoke CloseHandle,@hMapFile
      .endif
      invoke CloseHandle,@hFile
    .endif
  .endif
@@:        
  ret
_openFile endp
```

------

第44～59行代码的功能是检测打开的文件是否符合PE标准。第61～69行的代码的功能是调用不同的函数显示PE的相关信息。例如，PE的主要信息的显示调用了函数_getMainInfo：

------

```assembly
invoke _getMainInfo,@lpMemory,esi,@dwFileSize
```

------

#### 2._getMainInfo函数

该函数接收三个参数：

- ==`_lpFile`（内存映射文件的起始地址）==
- ==`_lpPeHead`（数据结构IMAGE_NT_HEADERS在内存中的起始位置）==
- ==`_dwSize`（PE文件大小）==

该函数获取PE文件的头部信息并显示，由于实现很简单，就不再详细分析了，如代码清单2-9所示。

代码清单2-9　获取PE文件主要信息的函数`_getMainInfo`（chapter2\peinfo.asm）

------

```assembly

;--------------------
; 从内存中获取PE文件的主要信息
;--------------------
_getMainInfo  proc _lpFile,_lpPeHead,_dwSize
  local @szBuffer[1024]:byte
  local @szSecName[16]:byte

  pushad
  mov edi,_lpPeHead
  assume edi:ptr IMAGE_NT_HEADERS
  movzx ecx,[edi].FileHeader.Machine          ;运行平台
  movzx edx,[edi].FileHeader.NumberOfSections ;节的数量
  movzx ebx,[edi].FileHeader.Characteristics  ;节的属性
  invoke wsprintf,addr @szBuffer,addr szMsg,\
         addr szFileName,ecx,edx,ebx,\
         [edi].OptionalHeader.ImageBase,\     ;含建议装入的地址
         [edi].OptionalHeader.AddressOfEntryPoint
  invoke SetWindowText,hWinEdit,addr @szBuffer;添加到编辑框中

  ;显示每个节的主要信息
  invoke _appendInfo,addr szMsgSec
  movzx ecx,[edi].FileHeader.NumberOfSections
  add edi,sizeof IMAGE_NT_HEADERS
  assume edi:ptr IMAGE_SECTION_HEADER
  .repeat
    push ecx
    ;获取节的名称，注意长度为8的名称并不以0结尾
    invoke RtlZeroMemory,addr @szSecName,sizeof @szSecName
    push esi
    push edi
    mov ecx,8
    mov esi,edi
    lea edi,@szSecName
    cld
    @@:
    lodsb
    .if !al  ;如果名称为0，则显示为空格
      mov al,' '
    .endif
    stosb
    loop @B
    pop edi
    pop esi
    ;获取节的主要信息
    invoke wsprintf,addr @szBuffer,addr szFmtSec,\
           addr @szSecName,[edi].Misc.VirtualSize,\
           [edi].VirtualAddress,[edi].SizeOfRawData,\
           [edi].PointerToRawData,[edi].Characteristics
    invoke _appendInfo,addr @szBuffer
    add edi,sizeof IMAGE_SECTION_HEADER
    pop ecx
  .untilcxz

  assume edi:nothing
  popad
  ret
_getMainInfo endp

```

------

其他信息（如导入表、导出表、资源表等）的显示代码将在后续的章节中详细介绍。

#### 2.4.3　运行PEInfo

编译链接生成PEInfo.exe，然后运行。用该程序打开第1章生成的HelloWorld.exe程序，运行效果见图2-6。

![image](https://github.com/YangLuchao/img_host/raw/master/20230725/image.4u6zpuvnlci0.webp)

图　2-6　PEInfo运行效果

至此，三个小工具的开发工作就完成了，在后续的章节中，会陆续使用这些小工具完成对PE格式的分析和学习。
