[toc]

> 所有的高级语言的异常处理都是依赖于操作系统异常处理
>
> 异常处理顺序：
>
> 1. 注册全局异常筛选器 -> 	处理全局异常
> 1. 调试器处理
> 1. SEH异常处理链
> 1. SEH异常处理链最后一个节点就是操作系统默认异常处理

==加载配置信息最初只用在Windows NT操作系统中，作为文件头部的延伸部分，后来被用做异常处理==。==加载配置信息表中存放了基于结构化异常处理（Structured Exception Handling，SEH）技术的各种异常句柄==。==当程序运行发生异常后，操作系统会根据异常类别对异常进行分发处理，并依据这些句柄实施程序流程的转向，保证系统能从异常中全身而退==。==本章将详细介绍Windows操作系统结构化异常处理机制，以及PE中与之对应的相关数据结构==。

# 10.1　何谓加载配置信息

==PE中的加载配置信息存储在一个名为加载配置结构（Load Configuration Structure）的数据结构中==。加载配置结构是PE中定义的一种基本数据类型，最初仅用于Windows NT操作系统，定义一些供Windows NT操作系统加载PE时用到的一些附加信息。这些信息之所以被单独定义，是因为这些信息太大，无法被标准PE文件头部和扩展文件头部的数据结构所容纳；有的则是因为信息类型太复杂，无法在标准PE文件头部和扩展头部中实现。

==当前版本的微软编译器和Windows XP操作系统，以及后出的其他操作系统中，该部分的含义发生了变化。不再定义加载用的配置信息，而是被专门用来定义基于SEH技术的相关数据，所以笔者更愿意称这一部分为异常处理表==。

==如果PE中的该部分表中没有对应的异常类别处理函数句柄，操作系统将会调用其内核模式的异常分发函数终止应用程序的运行==。==这种安全设置主要是为了阻止因异常句柄导致的溢出被恶意程序利用，从而造成对系统的破坏==。

==通常情况下，链接器会提供一个默认的加载配置结构，该结构包含了预留的SEH数据==。==如果用户代码中提供了该结构，则必须由用户来完成设置新的预留SEH字段，否则链接器不会将SEH数据加入到加载配置信息中==。

# 10.2　Windows结构化异常处理

在DOS时代，中断被设计为整个操作系统的核心。所有的中断（如系统的按键引发的中断）最后以“int功能号”的形式调用。系统会依据中断向量表查找系统中定义的中断入口，并跳转到此处执行该中断的相关代码。

在Windows操作系统中，异常（Exception）和中断（Interrupt）提供了类似的功能；所不同的是，==中断通常是由外部的条件引发，如按下了一个键==；==而异常则是代码或数据中的条件导致处理器生成的，异常与正在执行的指令有直接的联系==。==从宏观上讲，异常就是一种内部中断，因为当异常发生时，CPU会中断当前进程转到异常处理程序==。==这些处理程序的地址记录在一个被称为中断描述符表（IDT）的数据结构中，对应的处理程序存储在ntoskrnl.exe文件里==。

## 10.2.1　什么是SEH

有时候，异常和中断这两个概念是混淆的。比如，当程序需要读取某段数据时，对应的数据却已经被换出内存了，这时候就会产生缺页异常。在程序运行期产生了异常；系统通常的做法都是先尽力挽救该异常，实在没有办法才结束运行的进程。当发生缺页异常后，程序的运行被暂时中断，系统会做补救操作，比如将缺页的内容重新换回内存，然后回到被中断运行的程序重新读取这段数据。大多数情况下，这种异常会经常发生，且被操作系统很好地处理，程序也可以顺利地运行下去。其他异常的处理方式基本与上述的描述是一致的。

出于各种无法预料的原因，在程序运行时会发生很多异常，这些异常有些是可以恢复的，有些是不可以恢复的，有些可能被运行的程序本身发现并处理。图10-1是Word模板文件被病毒感染后引发的异常。

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.4wvl2nhwl7g0.png)

图　10-1　Word模板文件感染病毒后的异常处理

也有可能程序本身不提供异常处理，由操作系统接管异常。被操作系统接管的异常比较多，异常发生后操作系统将弹出提示框，如图10-2所示。

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.38g7xamw62a0.jpg)

图　10-2　操作系统接管异常处理的弹出窗口

==还有一种异常，程序不提供接管程序，操作系统提供的接管程序也无法管理，这种异常就是意外（这些意外一般是不可恢复的异常）==。==如电脑的硬件故障导致整个系统崩溃，崩溃时系统一般会表现为蓝屏、死机，用户只能关闭计算机或检测硬件设备后重新启动系统==。

在程序设计时，使用汇编语言的用户会通过类似如下的形式，在程序内部实现对异常的处理：

------

```assembly
Fun1 proc
	assume fs:nothing
	push offset _handler ;_handler为异常处理程序
	push fs:[0]
	mov fs:[0],esp
	…… ;程序实现代码
	pop fs:[0]
	pop eax
	ret
Fun1 endp
```

------

使用C语言的用户可能会通过类似以下的形式，在程序内部实现对异常的处理：

------

```c
Fun1
{
	_SEH_TRY
	{
		……//程序实现代码
	}
	_SEH_HANDLE
	{
		Status=_SEH_GetExceptionCode() ;//异常处理
	}
	_SEH_END ;
	if(!NT_SUCCESS(Status))
	{
		return Status ;
	}
}
```

------

使用Java语言的用户可能会使用如下的形式定义内部异常处理：

------

```java
public void Fun1{
	Try{
		……//程序实现代码
	}catch(Exception ex){
		……//异常处理
	}
}
```

------

==为了方便开发者处理异常，在程序内部异常的处理是基于块的。程序中的任何一个函数都可以定义异常处理模块，程序代码实现和异常处理被不同的伪操作语句隔离开==。

==这种在操作系统和程序级别上采用的，基于一块一块的结构化的异常处理方式称为结构化异常处理（Structured Exception Handling，SEH）==。

## 10.2.2　Windows异常分类

由于SEH使用了与硬件平台相关的数据指针，所以在不同的硬件平台上，SEH的实现方法是不同的。==在x86平台上的SEH处理框架中，把异常分为两大类==：

- ==硬异常（系统异常）==
- ==软异常（程序自己抛出的异常）==

### 1.硬异常

==硬异常，即系统异常，可以细分为以下三类==：

#### （1）==故障（Fault）异常==

==它是因执行指令失败而引起的，比如除以0引发的异常，以及eip指向了不可执行的页面等==，这一类异常是系统异常中最常见的。此类异常有一个共同点，那就是发生异常时自动压入栈的是失败指令的地址，而不是它的下一条指令的地址（注意，这与call指令是不一样的）。==这样做的原因是：当从异常遍历过程返回时，可以重新执行一遍这条指令==。

#### （2）==陷阱（Trap）异常==

发生这类异常通常是因为执行了自陷指令，如使用指令"INT 3"而引发的异常。==这一类异常的返回地址是自陷指令的下一条指令所在的地址==。

#### （3）==终止（Abort）异常==

==这类异常专指那些已经无法恢复的严重出错，如硬件故障引发的异常，或者系统表中出现了错误值引发的异常==。

表10-1是Intel CPU层级上常用的中断/异常号及对应的说明。

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.1f15ovikvj8g.jpg)

Windows操作系统使用了自己定义的一套代码来表示各种异常。操作系统会把相应的CPU异常代码映射到一个或多个通用的Win32异常代码上。如CPU的13号异常可能会变成STATUS_ACCESS_VIOLATION（0xC0000005），也可能变成STATUS_PRIVILEGED_INSTRUCTION（0xC0000096）异常。到底该异常会被映射到哪个Win32异常上由底层的硬件异常来决定。

### 2.软异常

所谓软异常，就是以函数调用的手段来模拟一次异常，即通过Windows提供的API函数RaiseException，执行函数引发软异常。它的声明如下：

------

```cpp
VOID RaiseException(
	DWORD dwExceptionCode,		//异常代码
	DWORD dwExceptionFlags,		//继续执行标志
	DWORD nNumberOfArguments,	//参数个数
	CONST DWORD*lpArguments		//指向参数缓冲区的指针
);
```

------

实际上，在高级语言的异常处理模型中的大部分抛出异常的操作，最终都是对RaiseException函数的调用。

## 10.2.3　==内核模式下的异常处理==

==在用户模式下，`fs:[0x00]`指向了线程的环境块（TEB）地址。该地址的第一个成分是线程信息块（TIB）结构，而ExceptionList则是这个结构的第一个字段，该字段是用户模式下异常处理链表的指针==。==当进入内核模式的异常处理程序后，fs被赋予了另外的含义==，代码如下：

------

```assembly
;Save FS and set it to PCR
push fs
mov ebx,KGDT_R0_PCR ;KGDT_R0_PCR=0x30
mov fs,bx
```

------

==在代码中，系统将用户模式下的原始fs值保存到栈，然后将fs指向了内核处理器控制域（Kernel Processor Control Region，KPCR）数据结构==。==可以看到，内核模式下和用户模式的fs概念基本是一样的==。==在内核模式下，fs:[0x00]也是一个异常处理链表的指针==。==KPCR结构中的第一个成分是KPCR_TIB数据结构，ExceptionList则是KPCR_TIB结构中的第一个字段==。以下是内核模式下两个数据结构的完整定义：

------

```assembly
typedef struct _KPCR_TIB{
	PVOID ExceptionList, ;0000h-指向_EXCEPTION_REGISTRATION_RECORD指针
	PVOID StackBase, ;0004h-栈基地址
	PVOID StackLimit, ;0008h-栈大小
	PVOID SubSystemTib, ;000C-
	_ANONYMOUS_UNION union{
		PVOID FiberData, ;0010h-
		DWORD Version ;0010h-
	} DUMMYUNIONNAME
	PVOID ArbitraryUserPointer, ;0014h-
	struct _NT_TIB*Self ;0018h-指向本体结构
}
typedef struct _EXCEPTION_REGISTRATION_RECORD
{
	struct _EXCEPTION_REGISTRATION_RECORD*Next, ;0000h-指向下一个相同结构
	PEXCEPTION_ROUTINE Handler ;0004h-SEH异常处理回调函数指针
}
```

------

==内核模式下的异常处理程序首先根据异常的类型构造一个陷阱框架（KTrapFrame）==。==该框架是一个数据结构，里面记录了当异常发生时的系统环境，如各寄存器的当前值、调试信息、错误代码、异常处理函数链表等==。==框架构造完成后,调用公共的`_KiTrapHandler`异常处理函数==。==该函数接收两个参数，一个是异常号，另外一个就是陷阱框架==。大致的代码如下：

------

```assembly
;Call the C exception handler
push 0 ;异常号
push ebp ;KTrapFrame起始指针
call _KiTrapHandler
add esp,8
```

------

==认为`_KiTrapHandler`是公共的异常处理函数，是因为大部分的异常处理都是把它当成内核异常的入口==。当然，也有例外，比如14号异常（页异常），该异常的处理入口为函数`_KiPageFaultHandler`。==公共的异常处理函数最终会根据CPU在发生异常时所处的地址空间而定==：==如果是用户层，调用函数`_KiUserTrapHandler`==；==如果是内核层，调用函数`\_KiKernelTrapHandler`==。以下是内核函数\_KiKernelTrapHandler的代码：

------

```c
ULONG
KiKernelTrapHandler(PKTRAP_FRAME Tf,ULONG ExceptionNr,PVOID Cr2)
{
	EXCEPTION_RECORD Er ;
	Er.ExceptionFlags=0 ;
	Er.ExceptionRecord=NULL ;
	Er.ExceptionAddress=(PVOID)Tf-＞Eip ;
	if(ExceptionNr == 14)//页异常需要单独处理
	{
		Er.ExceptionCode=STATUS_ACCESS_VIOLATION ;
		Er.NumberParameters=2 ;
		Er.ExceptionInformation [0]=Tf-＞ErrCode＆0x1 ;
		Er.ExceptionInformation [1]=(ULONG)Cr2 ;
	}
	else
	{
		if(ExceptionNr＜ARRAY_SIZE(ExceptionToNtStatus))
		{
			Er.ExceptionCode=ExceptionToNtStatus [ExceptionNr] ;
		}
		else
		{
			Er.ExceptionCode=STATUS_ACCESS_VIOLATION ;
		}
			Er.NumberParameters=0 ;
	}
	/*FIXME:Which exceptions are noncontinuable?*/
	Er.ExceptionFlags=0 ;
	KiDispatchException(＆Er,NULL,Tf,KernelMode,TRUE) ;
	return(0) ;
}
```

------

该函数构造了异常记录块EXCEPTION_RECORD，然后调用KiDispatchException。异常记录块的完整定义如下：

------

```assembly
typedef struct _EXCEPTION_RECORD{
	DWORD ExceptionCode, 	;异常代码
	DWORD ExceptionFlags, 	;异常的状态标志位
	struct _EXCEPTION_RECORD*ExceptionRecord, ;指向另一个异常记录块
	PVOID ExceptionAddress, ;本次异常的返回地址
	DWORD NumberParameters, ;数组ExceptionInformation []中有效数据个数
	DWORD ExceptionInformation [EXCEPTION_MAXIMUM_PARAMETERS] ;
}
```

------

==异常记录块用来记录一个异常所对应的相关信息，其中含有异常的代码、发生异常时的系统状况、异常之间的联系等。每一个异常发生时，系统都会传递这样一个数据结构==。函数KiDispatchException的原型为：

------

```c
NTAPI
KiDispatchException(
	PEXCEPTION_RECORD ExceptionRecord,//指向ExceptionRecord的指针
	PKEXCEPTION_FRAME ExceptionFrame,//对x86，为NULL
	PKTRAP_FRAME TrapFrame,//陷阱框架指针
	KPROCESSOR_MODE PreviousMode,//用户模式还是内核模式
BOOLEAN FirstChance//是否为进行的第一次努力
);
```

------

==该函数不仅是内核模式下异常处理最后调用的函数，也是用户模式下异常处理函数KiUserTrapHandler最后调用的函数。该函数试图通过执行三次尝试来处理异常==：

==**第一次尝试**，即FirstChance=1。异常会先提交给调试程序，如调试程序不存在或调试程序也不能解决该异常，就调用函数RtlDispatchException进行实质性的SEH处理==。==SEH机制对异常的处理有以下三种可能==：

==如果异常被某个SEH框架所接受，并实施了长程跳转（跳转到异常处理程序执行异常处理），程序就不再返回了==。

==如果异常被某个SEH框架所接受，但是程序认为对该异常的处理只需要执行善后函数即可，这样，程序就会从RtlDispatchException返回，并且其返回的值是TRUE==。

==如果异常被所有的SEH框架都拒绝接受，那就意味着处理异常的第一次尝试失败==。

==**第二次尝试**，第一次尝试失败后，程序将再次提交给调试程序。通过调用其他的调试支持判断是否可以处理该异常。如果这一次取得了成功，问题解决了，那么返回值是常数kdContinue；否则进入第三次尝试==。

==**第三次尝试时**，表示系统已经没有办法处理这个故障了，所以系统会显示出错信息，并将出错信息转储（Dump）到文件中以备事后分析，然后使CPU进入停机状态==。通常情况下，此时系统将显示蓝屏，并显示类似于下面的一些信息：

------

```cpp
***STOP 0x0000001E(0xC0000005,0xFDE38AF9,0x0000001,0x7E8B0EB4)
KMODE_EXCEPTION_NOT_HANDLED***
```

------

==SEH处理的核心就是对ExceptionList（异常处理链表）的扫描处理，这是由函数RtlDispatchException来完成的，对该函数的调用位于函数KiDispatchException中==。

==事实上，绝大多数的异常都可以通过这个函数得到妥善的处理==。==函数首先通过RtlpGet ExceptionList找到异常处理链表，即当前CPU的KPCR结构中的指针ExceptionList==；==然后，通过一个while循环来依次搜寻处理ExceptionList链表中的每一个节点，由RtlpExecuteHandlerForException加以尝试处理==。==链表中的每个节点都代表一个局部SEH框架==。

==由于异常处理链表是个后进先出的队列，里面的第一个节点代表最近进入的SEH框架==；==如果链表中有不止一个的节点，就说明有了SEH框架嵌套==。==在嵌套的情况下，队列中的第一个节点代表最底层的那个保护域==；==如果这个节点（执行过滤函数以后）拒绝接受本次异常处理，就说明并非这个SEH域所针对的异常，那就应该往栈顶再“跑”一层，看是否为上一层SEH域所针对的异常==；==如此反复，直到在栈上找到某个节点接受本次异常为止。当一个节点接受了对异常的处理，通常会执行预先规定的长程跳转，直接“跑”到那个框架中的相关语句部分，即由字段`_EXCEPTION_REGISTRATION_RECORD.Handler`指定的代码==。==在长程跳转之前还需要有个“展开”（Unwinding）的过程，那就是调用所有被跨越的SEH框架的善后函数，这就是为什么在有的错误处理中会看到出现两次相同的错误提示的原因了==。函数RtlpExecuteHandlerForException的返回值及说明见表10-2。

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.razdronro28.jpg)

==如果while循环结束，说明异常处理链表中所有的节点都不是为本类异常准备的。也就是说，程序事先并没有估计到本类异常的发生，也没有为此做出更多的安排，所以返回FALSE，让上一层KiDispatchException采取其第二步措施==。

内核模式下异常处理函数的调用关系如下：

------

```cpp
_KiTrapHandler
_KiKernelTrapHandler
_KiDispatchException ;三次尝试
_RtlDispatchException
_RtlpGetExceptionList
_RtlpExecuteHandlerForException ;尝试循环执行异常处理
```

------

## 10.2.4　==用户模式下的异常处理==

如前所述，==异常是中断的一种，不管是由什么原因（“软异常”除外）引发的异常，一旦发生首先进入的是内核中的异常响应/处理程序的入口，不同的原因引发的异常会有不同的处理入口，就像不同的中断向量会有不同的入口地址一样==。

==异常发生进入内核处理程序后，程序就会顺着内核中的KPCR数据结构中的“异常处理队列即`ExceptionList`，依次让各个节点认领==。==如果异常被某个节点接受，那么程序就会通过SEHLongJmp长程跳转到节点给定的异常处理代码中==。==用户模式下的异常处理采用类似的方式及相关数据结构，数据结构主要指线程信息块`NT_TIB`==，以下是该数据结构的完整定义：

------

```assembly
NT_TIB STRUCT
	ExceptionList dd ? 	;0000h-指向SEH链的入口
	StackBase dd ? 		;0004h-栈基地址
	StackLimit dd ? 	;0008h-栈大小
	SubSystemTib dd ? 	;000Ch-
	FiberData dd ? 		;0010h-
	ArbitraryUserPointer dd ? ;0014h-
	Self dd ? 			;0018h-NIT_TIB结构自身的线性地址
NT_TIB ENDS
```

------

其中，`ExceptionList`指向一个`EXCEPTION_REGISTRATION`结构，该结构完整定义如下：

------

```assembly
EXCEPTION_REGISTRATION STRUCT
	Prev dd ? ;0000h-前一个EXCEPTION_REGISTRATION结构的地址
	Handler dd ? ;0004h-异常处理回调函数地址
EXCEPTION_REGISTRATION ENDS
```

------

==当异常发生的时候，系统从`NT_TIB`中取出第一个字段，然后依据该字段获取第一个异常处理程序的句柄Handler，并根据其中的地址调用该回调函数==。图10-3为用户模式下异常情况发生时的异常处理过程示意图。

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.1q0sb8p0c8io.jpg)

图　10-3　用户模式下SEH异常处理过程

==异常发生于用户空间，在内核中的异常处理程序就会由内核态函数`KiKernelTrapHandler`改变为用户态函数`KiUserTrapHandler`==。==该函数构造异常记录块，然后转交给函数`KiDispatchException`来处理==。

==在用户模式下，当通过`NtCreateThread`创建的线程首次被调度运行时，整个线程的执行都是作为一个SEH域而受到保护==。==函数`BaseProcessStartup`是所有线程在用户空间的总入口，该函数传递的参数`lpStartAddress`则是具体线程的代码入口==，函数仿真代码如下：

------

```cpp
VOID STDCALL
BaseProcessStartup(PPROCESS_START_ROUTINE lpStartAddress)
{
	UINT uExitCode=0 ;
	_SEH_TRY
	{

		/*设置起始地址*/
		NtSetInformationThread(NtCurrentThread(),ThreadQuerySetWin32StartAddress, ＆lpStartAddress,sizeof(PPROCESS_START_ROUTINE)) ;

		/*调用启动例程*/
		uExitCode=(lpStartAddress)() ;
	}

	_SEH_EXCEPT(BaseExceptionFilter)
	{
		/*获取SEH异常代码*/
		uExitCode=_SEH_GetExceptionCode() ;
	}

	_SEH_END ;
	/*带着异常代码退出进程*/
	ExitProcess(uExitCode) ;
}
```

------

注意，异常处理框架中引用了`_SEH_EXCEPT`，这不是一个双字节的句柄值`_SEH_HANDLE`，它是过滤函数`BaseExceptionFilter`；该函数又通过另外一个函数指针调用实际的过滤函数，默认为`UnhandledExceptionFilter`。函数`UnhandledExceptionFilter`在一般情况下都返回`EXCEPTION_EXECUTE_HANDLER`。不过，应用程序可以也通过`SetUnhandledExceptionFilter`将其替换成自己想要的过滤函数。

==用户模式下只有一个类似于内核空间的`KiDispatchException`函数，它就是动态链接库`ntdll.dll`中的`KiUserExceptionDispatcher`，它是用户模式下SEH异常处理的总入口==。

==尽管是发生于用户空间的异常，对异常的初期响应和处理还都是在内核中进行的。那么在内核中的程序是如何进入并启动用户空间函数的呢==？下面让我们转到内核函数`KiDispatchException`中去看个究竟：

==内核中涉及用户空间异常的处理也分三步==：

**步骤1**　==参数`FirstChance`为1时，先通过`KdpEnterDebuggerException`交由内核调试程序处理。如果内核调试程序解决了该问题，或者它认为不需提交回用户空间，返回值就是常量`kdContinue`==；==否则就要把异常提交给用户空间，由用户空间的程序加以处理==。

**步骤2**　==万一用户空间处理不了，例如`ExceptionList`中没有安排可以认领、处理本次异常的节点，就会通过调用函数`RtlRaiseException`，进而通过系统调用`ZwRaiseException`发起一次“软异常”，把问题重新交还给内核==。==此时，CPU再次进入`KiDispatchException`，但是此时的实际参数`FirstChance`被改为FALSE，所以直接进入第二步措施==。==在Windows内核中，第二次尝试是通过进程间通信向用户空间的调试程序发送一个报文，将其唤醒，由调试程序作进一步的处理==。

**步骤3**　==如果用户空间调试程序不存在，或者也不能解决，那就属于不可恢复的问题了==。==于是就有第三步措施，第三步措施会调用`ZwTerminateThread`结束当前线程的运行==。

==内核模式下程序把异常提交给用户空间的步骤如下==：

**首先**，==把上下文数据结构`Context`和异常记录块`ExceptionRecord`复制到用户空间的栈上去==。

**其次**，==在用户空间栈上压入两个指针，分别指向这两个数据结构的用户空间副本，并相应调整异常框架中的用户空间栈指针==。

**最后**，==也是最关键的一步，把异常框架中的用户空间返回地址设置成函数指针`KeUserExceptionDispatcher`所指向的函数==。

==当CPU从异常返回，回到用户空间时就进入了函数`KiUserExceptionDispatcher`，该函数就是用户模式下的异常响应/处理程序的入口==。==与内核中异常有多个入口不同，用户空间有且仅有一个这样的入口==。至于绑定到异常上的其他信息，则由异常记录块负责记录。

==用户空间的异常机制是对系统空间的异常机制的模拟==。==在内核中，并非所有的异常都是一开始就进入“基于SEH框架”(Frame-Based)的异常处理，而是先进入类似向量中断的入口==。==用户空间也一样，在用户空间有一个函数，它负责遍历一个“向量式异常处理程序入口队列”==。==如果异常被这些队列中的某个处理程序认领，则不再进行基于SEH框架的异常处理，该函数是全局性的，在`RtlDispatchException`执行前运行==。以下是函数`KiUserExceptionDispatcher`的调用过程：

------

```c
KiUserExceptionDispatcher()
RtlDispatchException()
RtlpIsValidHandler() ;异常处理函数指针安全验证
RtlpExecuteHandlerForException() ;尝试处理异常
ExecuteHandler()
```

------

其中，函数`RtlpIsValidHandler`提供了代码指针的安全验证，其仿真代码如下：

------

```cpp
BOOL RtlpIsValidHandler(handler)
{
    if (handler is in an image)
    {
        if (image has the IMAGE_DLLCHARACTERISTICS_NO_SEH flag set)
            return FALSE;
        if (image has a SafeSEH table)
            if (handler found in the table)
                return TRUE;
            else
                return FALSE;
        if (image is a.NET assembly with the ILonly flag set)
            return FALSE;
        // 其他分支，则通过
    }
    if (handler is on a non - executable page)
    {
        if (ExecuteDispatchEnable bit set in the process flags)
            return TRUE;
        else
            // 执行DEP
            raise ACCESS_VIOLATION;
    }
    if (handler is not in an image)
    {
        if (ImageDispatchEnable bit set in the process flags)
            return TRUE;
        else
            return FALSE; // 不能允许句柄在映像以外
    }
    // 其他任何情况都是被允许的
    return TRUE;
}
[...]
    // 如果DisableExceptionChainValidation位被设置，则跳过链表验证
    if (process _flags＆0x40 == 0)
{
    // 如果在链表上没有SEH records，则跳过验证
    if (record != 0xFFFFFFFF)
    {
        // 遍历SEH链表
        do
        {
            // record必须在栈上
            if (record＜stack _bottom || record＞stack _top)
                goto corruption;
            // record的结束也必须在栈上
            if ((char *)record + sizeof(EXCEPTION_REGISTRATION)＞stack _top)
                goto corruption;
            // record必须是4字节对齐
            if ((record＆3) != 0)
                goto corruption;
            handler = record -＞handler;
            // 句柄必须在栈上
            if (handler＞ = stack _bottom＆＆handler＜stack _top)
                goto corruption;
            record = record -＞next;
        } while (record != 0xFFFFFFFF);
        // 到达链表尾部
        // TEB-＞SameTebFlags字段的第9位是否被设置
        // 该位将在函数ntdll!RtlInitializeExceptionChain中被设置
        // 当一个新的线程开始时，它将FinalExceptionHandler注册为一个SEH句柄
        if ((TEB -＞word _at _offset_0xFCA＆0x200) != 0)
        {
            // 最后的句柄一定是ntdll!FinalExceptionHandler
            if (handler !=＆FinalExceptionHandler)
                goto corruption;
        }
    }
}
```

------

如上所示，函数`RtlpIsValidHandler`检查SEH handler的过程包括：

1. **步骤1**　==检查handler是否在线程环境块TEB指定的Stack范围内（fs:[4]～fs:[8]），如果是则拒绝执行==。

2. **步骤2**　==检查handler是否在已加载模块列表（exe和dll）中，如果handler不在这些模块地址范围内，则拒绝执行==。

3. **步骤3**　==如果handler在模块地址范围内，则检查已注册异常处理程序列表==。


检查过程如下：

**1）**if((DLLCharacteristics＆0xFF00) == 0x0400)，则拒绝执行（No SEH），否则继续检查。

**2）**数据目录项中的加载配置地址为0，也就意味着不存在IMAGE_LOAD_CONFIG_DIRECOTRY结构，说明编译时没有设置-safeseh选项，则停止检查，执行。

如果IMAGE_LOAD_CONFIG_DIRECOTRY结构存在，则继续检查以下字段：

+0000h：directory _size。首先判断目录长度是否介于0～0x48之间，是则停止检查，执行。

+0040h：handlers[]。其次检查SEH Handler的数组指针（元素是SEH Handler RVA）。如果该指针指向0，即if(handlers[] == 0)成立，则停止检查，执行。

+0044h：handler _num。最后检查SEH Handler数组元素个数，if(handler _num == 0)，则停止检查，执行。

**3）**根据SEH Handler开始逐个匹配，发现匹配则调用；否则拒绝调用。

==简言之，如果异常处理程序的地址在映像的VA范围之内，并且映像被标记为支持保留的SEH（也就是说，可选文件头中的`DllCharacteristics`字段没有设置`IMAGE_DLLCHARACTERISTICS_NO_SEH`标志），那么这个异常处理程序必须在映像的已知安全异常处理程序列表中（加载配置信息的结构字段`SEHandleTable`指向该列表），否则操作系统将终止应用程序==。对异常处理程序如此严格的限制主要目的是防止利用“x86异常处理程序劫持”来控制操作系统，这种技术在以前曾经被人利用过。

==在用户空间执行完“向量式异常处理程序入口队列”遍历后，如果没有找到异常的认领处理程序，则继续运行函数`RtlDispatchException`==。==与内核模式中的同名函数不同，此处的`RtlDispatchException`函数所处理的`ExceptionList`是在用户空间，所使用的栈也是用户空间栈==。==在内核模式部分我们分析过`fs:[0x00]`的内容，无论是在内核还是在用户模式下，其取到的都是数据结构`ExceptionList`指针==。

## 10.2.5　Windows SEH机制解析

==通过以上对内核模式和用户模式下基于SEH技术的异常处理过程。总结如下==：

==SEH机制就是在执行某一段代码的过程中，如果发生了特定种类的异常，就执行另一段指定代码==。==为实现结构化异常处理，Windows在系统空间和用户空间都有一个后进先出的异常处理队列ExceptionList。每当程序进入一个SEH框架时，就把一个带有长程跳转目标地址的数据结构挂入相应空间的异常处理队列，成为其一个节点==。==在内核空间将节点挂入系统空间的队列，在用户空间则挂入用户空间的队列==。

==一般而言，当CPU运行于用户空间时，系统空间的异常处理队列应该是空的==。==除长程跳转目标地址外，挂入`ExceptionList`的数据结构中还可以有两个函数指针==：

**1）**==过滤（Filter）函数的指针==。==这个函数判断所发生的异常是否就是本保护域所要处理的异常，如果是才加以接受，从而执行本SEH域的长程跳转，执行异常处理函数==。

**2）**==善后（Final）函数的指针==。==在进行异常认领过程中，将会遍历并执行节点上的各异常函数；在执行过程中，这些异常处理函数（无论它是否接受了该异常的处理）可能会申请一些资源，这些资源必须得到释放。善后函数的主要目的通常是释放展开过程动态获取的资源==。

==当发生异常时，异常响应程序就依次考察相应`ExceptionList`中的各个节点，并执行其过滤函数（如果有的话）==。如果过滤函数认为这就是本保护域所针对的异常，或者默认为相符不需要进行过滤，就执行本保护域的长程跳转，进入本SEH域的`_SEH_HANDLE{}`里面的代码；==而对于被跨越的各个内层SEH域，则执行其善后函数（如果有的话），即展开操作==。

==Windows对于段寄存器fs有特殊的设置和使用，当CPU运行于系统空间时，就使`fs:0`指向当前CPU的KPCR数据结构==。==而KPCR结构的第一个成分是`KPCR_TIB`数据结构，`KPCR_TIB`的第一个成分则是`VOID`指针`ExceptionList`==。

==当CPU运行于系统空间时，就使`fs:0`指向当前线程的`TEB`。`TEB`数据结构中的第一个成分是`NT_TIB`数据结构，这里面的第一个成分即指针`ExceptionList`==。

## 10.2.6　SEH编程实例

了解了SEH技术之后，下面通过一个SEH的实例程序来深入理解SEH机制，程序代码见代码清单10-1。

代码清单10-1　异常处理测试（chapter10\exception1.asm）

------

```assembly
;------------------------
; 测试异常处理
; 戚利
; 2011.1.19
;------------------------
    .386
    .model flat,stdcall
    option casemap:none

include    windows.inc
include    user32.inc
includelib user32.lib
include    kernel32.inc
includelib kernel32.lib

;数据段
    .data
szText     db  'HelloWorldPE',0
szErr      db  'SEH Error',0
;代码段
    .code
;自定义的异常处理函数
_handler proc _lpException,_lpSEH,\
             _lpContext,_lpDispatcherContext
  nop
  pushad
  mov esi,_lpException
  mov edi,_lpContext
   
  assume edi:ptr CONTEXT

  invoke MessageBox,NULL,addr szErr,NULL,MB_OK

  mov [edi].regEip,offset _safePlace
  assume edi:nothing

  popad  

  ;测试一
  ;发生的异常已被该函数接管
  mov eax,ExceptionContinueExecution

  ;测试二
  ;发生的异常未被该函数接管
  ;mov eax,ExceptionContinueSearch
  ret
_handler endp

start:
    assume fs:nothing
    push offset _handler
    push fs:[0]
    mov fs:[0],esp

    xor eax,eax
    mov dword ptr [eax],eax

_safePlace:

    pop fs:[0]
    pop eax

    invoke MessageBox,NULL,addr szText,NULL,MB_OK
    invoke ExitProcess,NULL
    end start
```

------

行23～47，是我们自己定义的异常回调处理函数。该函数首先显示一条信息，然后修改eip指针，使得异常处理程序完成后能跳过发生错误的行（即主程序中的第56行），指向后续的`_safePlace`标签所处的指令。在主程序中，通过为地址`ds:[00000000]`处赋值引发访问越界错误。

该异常代码为`EXCEPTION_ACCESS_VIOLATION`，其十六进制值0C0000005h，将这个异常代码值按位拆开，来分析它的各个bit位字段的含义：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.3u3rcijn6xo0.jpg)

第30位和第31位都是1，表示该异常是一个严重的错误，线程可能不能够继续往下运行，必须及时处理恢复这个异常。第29位是0，表示系统中已经定义了异常代码。第28位是0，留待后用。第16～27位是0，表示是FACILITY_NULL设备类型，它代表存取异常可发生在系统中任何地方，不是使用特定设备才发生的异常。第0～15位的值为5，表示异常错误的代码。也就是说，真正的异常代码只是用了低16位。在内核模式的异常处理程序中可以看到这一点：

------

```assembly
#define TRAP_PROLOG(Label)

;Just to be safe,clear out the HIWORD,since it's reserved
mov word ptr [esp+2],0 ;将异常代码的高16位清0
;Save the non-volatiles
push ebp
push ebx
push esi
push edi
;Save FS and set it to PCR
push fs
mov ebx,KGDT_R0_PCR
mov fs,bx
```

------

行50～53是该源代码中至关重要的一部分。首先，通过指令`assume fs:nothing`启动fs段寄存器；然后，将SEH回调函数（异常处理函数）的指针送往栈。此时，栈顶指针`esp`指向该地址。指令`push fs:[0]`是用来保存最初的值，即SEH链表上的上一层元素。指令`mov fs[0],esp`实际上是等于将异常处理函数的指针给了`EXCEPTION_REGISTRATION`的第一个字段，即`ExceptionList`字段，该字段为SEH链的入口。

行39～41，回调函数接管了该异常后对系统的声明，即告诉系统该异常已被我认领，我已对其进行了处理，其他的异常接管函数不需要再工作了。

行43～45同样是接管声明，它告诉系统：该异常我处理不了，请将异常交给下一层SEH框架处理。

两种不同的接管方式会出现两种不同的运行结果：前者会出现两个对话框，分别来自异常处理回调函数和主程序中的异常代码后的部分；后者也出现两个对话框，分别来自异常处理回调函数和系统的出错提示。

请读者使用OD单步调试exception1.exe，当执行到异常触发指令时的栈情况见表10-3。

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.5hvaoer687o0.jpg)

如表10-3所示，压入栈的SE处理程序和在`fs:0`处记录的一样。SEH框架中当前SE处理程序的指针指向代码清单10-1第23行`_handler`程序入口地址，而链表指针则指向了下一个SE处理程序的地址（0012FFE0）。此地址是一个栈地址，通过查找表10-3可以看到；该地址处指向了SEH链尾最后一个记录，也就是程序运行开始时最原始的SE处理程序。该部分程序位于kernel32.dll中。跟踪异常处理，直到OD返回用户的代码空间：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.20mtrimpihcw.jpg)

以上代码位于ntdll.dll空间中，0x7C9232A6处的"CALL ECX"即为跳转到用户定义的SEH异常处理回调函数的指令。以上反汇编序列比较符合内核代码中的标签`_RtlpExecuteHandler2@20`所指向位置的代码，如下所示：

------

```assembly
_RtlpExecuteHandler2@20:
/*Set up stack frame*/
push ebp
mov ebp,esp
/*Save the Frame*/
push [ebp+0xC]/*指向原节点、这是要保护的目标节点*/
/*Push handler address*/
push edx/*成为新节点中的Handler指针，指向保护函数*/
/*Push the exception list*/
push [fs:TEB_EXCEPTION_LIST]/*成为新节点中的Next指针*/
/*Link us to it*/
mov [fs:TEB_EXCEPTION_LIST],esp/*让ExceptionList指向新节点*/
/*Call the handler*/
push [ebp+0x14]
push [ebp+0x10]
push [ebp+0xC]
push [ebp+8]
mov ecx,[ebp+0x18]/*参数ExceptionHandler*/
call ecx/*调用ExceptionHandler，4个调用参数*/
/*Unlink us*/
mov esp,[fs:TEB_EXCEPTION_LIST]
/*Restore it*/
pop [fs:TEB_EXCEPTION_LIST]/*新节点已从ExceptionList中摘除*/
/*Undo stack frame and return*/
mov esp,ebp/*新节点不复存在于栈上*/
pop ebp
ret 0x14
```

------

执行完异常处理回调函数后，重新回到ntdll.dll空间，执行以下代码：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.69g7h386now0.jpg)

按F7键进入：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.14pr2eqqb6xs.jpg)

通过存根代码进入到内核模式执行：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.bzol0s2ddhc.jpg)

通过SYSTENTER指令进入内核模式，并交由内核模式代码（位于函数RtlDispatch Exception()中）跳转到用户空间`_safePlace`指向的位置处执行：

------

```cpp
/*Call the handler*/
DPRINT("Executing handler:%p\n",RegistrationFrame-＞Handler) ;
ReturnValue=RtlpExecuteHandlerForException(ExceptionRecord,
RegistrationFrame,Context,＆DispatcherContext,
RegistrationFrame-＞Handler) ;
DPRINT("Handler returned:%p\n",(PVOID)ReturnValue) ;
```

------

使用OD调试exception2_1.exe，看以下代码：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.6l7b2gpm7n80.jpg)

ECX中存放了与exception2.exe调试时不同的值，这说明该程序执行了不同的SEH异常处理分支程序。exception2.exe中此处的值指向了用户代码，而这里却指向kernel32.7C839AD8，最终，系统是通过调用kernel32.UnhandledExceptionFilter，并且转入函数ntdll.ZwRaiseException完成了相关的异常处理。

完成异常处理后，显示一个友好的出错信息提示，不再返回用户程序。所以，用户程序的标签`_safePlace`处的代码根本就没有执行的机会。

# 10.3　PE中的加载配置信息

本节将以exception4_1.exe文件为例，讲解PE文件中的加载配置信息。==首先，通过PE数据目录表定位到加载配置信息==；==然后，分析该部分数据对应的数据结构==；==最后，通过一个实例分析，描述了数据结构中每个字段与提取的字节码之间一一对应的关系==。

## 10.3.1　加载配置信息定位

==加载配置数据为数据目录中注册的数据类型之一，其描述信息处于数据目录的第11个目录项中==。使用PEDump小工具获取chapter10\exception4_1.exe的数据目录表内容如下：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.bw4z329cqfs.jpg)

加黑部分即为加载配置信息的数据目录内容。通过以上字节码得到如下信息：

加载配置信息所在地址RVA=0x000001000

加载配置信息数据大小=00000040h

为了避免SEH框架缓冲区溢出执行用户代码导致安全隐患，自XP系统以后的大部分系统中的PE文件，如常见的记事本程序、kernel32.dll、user32.dll等都附加了加载配置信息。通常情况下，操作系统的加载器会根据数据目录表中定义的大小，来判断加载配置信息的类别。为了保持对Windows XP操作系统和以前的操作系统的兼容，建议大家将此处大小的值设置为64字节，即十六进制的0x40。

以下是使用小工具PEInfo获取到的该文件所有的节信息：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.5xpayxm6xcs0.jpg)

根据RVA与FOA的换算关系，可以得到：

加载配置信息所在文件的偏移地址为0x00000400。

## 10.3.2　加载配置目录IMAGE_LOAD_CONFIG_DIRECTORY

加载配置信息的数据起始为数据结构`IMAGE_LOAD_CONFIG_DIRECTORY`。该结构的完整定义如下：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.1j7t36hmu528.jpg)

------

以下是各字段的详细解释。

------

#### 98.IMAGE_LOAD_CONFIG_DIRECTORY.Characteristics

+0000h，双字。==标志字节，用来显示文件的属性，通常为0。如果存在加载配置信息，则大部分情况下被设置为48h==。

#### 99.IMAGE_LOAD_CONFIG_DIRECTORY.TimeDateStamp

+0004h，双字。时间戳，这个值表示从UTC时间1970年1月1日午夜（00:00:00）以来经过的总秒数，它是根据系统时钟算出的。可以用C运行时函数来获取它。

#### 100.IMAGE_LOAD_CONFIG_DIRECTORY.MajorVersion

+0008h，单字。主版本。

#### 101.IMAGE_LOAD_CONFIG_DIRECTORY.MinorVersion

+000Ah，单字。次版本。

#### 102.IMAGE_LOAD_CONFIG_DIRECTORY.GlobalFlagsClear

+000Ch，双字==。当PE加载器加载该映像时需要清除的全局标志==。

#### 103.IMAGE_LOAD_CONFIG_DIRECTORY.GlobalFlagsSet

+0010h，双字。==PE加载器加载该映像时需要设置的全局标志==。

#### 104.IMAGE_LOAD_CONFIG_DIRECTORY.CriticalSectionDefaultTimeout

+0014h，双字。==用于这个进程处于无约束状态的临界区的默认超时值==。

#### 105.IMAGE_LOAD_CONFIG_DIRECTORY.DeCommitFreeBlockThreshold

+0018h，双字。==返回到系统之前必须释放的内存数量（以字节计）==。

#### 106.IMAGE_LOAD_CONFIG_DIRECTORY.DeCommitTotalFreeThreshold

+001Ch，双字。==空闲内存总量（以字节计）==。

#### 107.IMAGE_LOAD_CONFIG_DIRECTORY.LockPrefixTable

+0020h，双字。该值是一个VA。该字段仅适用于x86平台。它指向一个地址列表，这个地址列表中保存的是使用lock前缀的指令的地址，这样便于在单处理器机器上将这些lock前缀替换为nop指令。

#### 108.IMAGE_LOAD_CONFIG_DIRECTORY.MaximumAllocationSize

+0024h，双字。==最大的分配粒度（以字节计）==。

#### 109.IMAGE_LOAD_CONFIG_DIRECTORY.VirtualMemoryThreshold

+0028h，双字。==最大的虚拟内存大小（以字节计）==。

#### 110.IMAGE_LOAD_CONFIG_DIRECTORY.ProcessAffinityMask

+002Ch，双字。如果将该字段设置为非零值，则等效于在进程启动时将这个设定的值作为参数去调用函数SetProcessAffinityMask。

#### 111.IMAGE_LOAD_CONFIG_DIRECTORY.ProcessHeapFlags

+0030h，双字。==进程堆的标志，相当于函数HeapCreate的第一个参数。这些标志用于在进程启动过程中创建的堆==。

#### 112.IMAGE_LOAD_CONFIG_DIRECTORY.CSDVersion

+0034h，单字。Service Pack版本标识。

#### 113.IMAGE_LOAD_CONFIG_DIRECTORY.Reserved1

+0036h，单字。保留值。

#### 114.IMAGE_LOAD_CONFIG_DIRECTORY.EditList

+0038h，双字。保留，供系统使用。

#### 115.IMAGE_LOAD_CONFIG_DIRECTORY.SecurityCookie

+003Ch，双字。指向cookie的指针。该cookie由Visual C++编译器的"GS implementation"所使用。

#### 116.IMAGE_LOAD_CONFIG_DIRECTORY.SEHandlerTable

+0040h，双字。==该值为一个VA。与平台相关，指向一个地址列表。这个地址列表中保存的是映像中每个合法的、独一无二的、基于SEH框架的异常处理程序的RVA，并且它们已经按RVA从小到大的顺序排过序，最后以一个双字的0结尾==。

#### 117.IMAGE_LOAD_CONFIG_DIRECTORY.SEHandlerCount

+0044h，双字。==IMAGE_LOAD_CONFIG_DIRECTORY.SEHandlerTable指向列表中双字的个数，最后一个0除外==。

## 10.3.3　加载配置信息实例分析

下面来看exception4_1.exe的加载配置信息：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.33tbng50nnc0.jpg)

指向`safe Handlers`列表的指针。该值为`VA`，转换为文件偏移是`0x00000448`。从这个位置取出的值（个数由SEHandlerCount来确定）是：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.6qgcwey1br80.jpg)

这些值都是RVA，将这些值转换为VA并从对应内存地址取出的反汇编代码依次为：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.1g0svgkbmbkw.jpg)

注册的异常处理回调函数的个数，本例中为2。

# 10.4　加载配置编程

本节通过代码为应用程序PE映像中附加“加载配置”信息。加载配置信息中首先注册源代码中定义的安全SEH回调函数；然后，通过在主程序中构造一个异常，并由主程序自己截获该异常，从而转到相应的异常处理函数处理该异常。

程序中将会存在两个异常处理函数：一个显示`safeHandler`，是要注册到加载配置信息中的函数；另外一个函数将不被注册，即不附加到安全句柄表`IMAGE_LOAD_CONFIG_DIRECTORY.SEHandlerTable`中。

读者可以通过分析程序的最终运行效果，来理解异常处理表在PE文件中的应用。首先看演示用的异常处理函数程序的源代码。

## 10.4.1　程序源代码分析

本节的演示程序通过在代码段构造`IMAGE_LOAD_CONFIG_DIRECTORY`结构，并为特定字段赋值的方法，实现SE程序的注册。本示例旨在向读者演示如何在应用程序中为最终生成的PE文件添加加载配置信息，以及这些信息是如何参与程序中的异常处理的。详细代码见代码清单10-2。

代码清单10-2　SEH异常处理演示（chapter10\exception4.asm）

```assembly
;------------------------
; 测试异常处理
; 指定了一个safe SEH Handler
; 并测试该异常处理函数，运行后会显示两个提示信息
; 一个是异常处理函数的提示信息，
; 另外一个是异常被处理后主程序的提示信息
; 戚利
; 2011.2.15
;------------------------
    .386
    .model flat,stdcall
    option casemap:none

include    windows.inc
include    user32.inc
includelib user32.lib
include    kernel32.inc
includelib kernel32.lib

;数据段
    .data
szText1     db  'safeHandler!',0
szText2     db  'nosafeHandler!',0
szText      db  'HelloWorldPE',0

;代码段
    .code

;IMAGE_LOAD_CONFIG_STRUCT STRUCT
    Characteristics dd                  00000048h
    TimeDateStamp dd                    0
    MajorVersion dw                     0
    MinorVersion dw                     0
    GlobalFlagsClear dd                 0
    GlobalFlagsSet dd                   0
    CriticalSectionDefaultTimeout dd    0
    DeCommitFreeBlockThreshold dd       0
    DeCommitTotalFreeThreshold dd       0
    LockPrefixTable dd                  0
    MaximumAllocationSize dd            0
    VirtualMemoryThreshold dd           0
    ProcessHeapFlags dd                 0
    ProcessAffinityMask dd              0
    CSDVersion dw                       0
    Reserved1 dw                        0
    EditList dd                         0
    SecurityCookie dd                  00000000h
    SEHandlerTable dd                  offset safeHandler ;(VA地址)
    SEHandlerCount  dd                 00000001h
;IMAGE_LOAD_CONFIG_STRUCT ENDS

;构造RVA
safeHandler      dd    offset _handler1-00400000h
                 dd    0


;-------------------------------------------
; 已注册的异常回调函数
;-------------------------------------------
_handler1 proc _lpException,_lpSEH,\
             _lpContext,_lpDispatcherContext
  nop
  pushad
  mov esi,_lpException
  mov edi,_lpContext
   
  assume edi:ptr CONTEXT

  invoke MessageBox,NULL,addr szText1,NULL,MB_OK

  mov [edi].regEip,offset _safePlace
  assume edi:nothing

  popad  

  mov eax,ExceptionContinueExecution
  ret
_handler1 endp

;-------------------------------------------
; 未注册的异常回调函数
;-------------------------------------------
_handler2 proc _lpException,_lpSEH,\
             _lpContext,_lpDispatcherContext
  nop
  pushad
  mov esi,_lpException
  mov edi,_lpContext
   
  assume edi:ptr CONTEXT

  invoke MessageBox,NULL,addr szText2,NULL,MB_OK

  mov [edi].regEip,offset _safePlace
  assume edi:nothing

  popad  
  mov eax,ExceptionContinueExecution
  ret
_handler2 endp

start:
    assume fs:nothing
    push offset _handler1
    push fs:[0]
    mov fs:[0],esp

    xor eax,eax  ;引发越界异常
    mov dword ptr [eax],eax

_safePlace:

    pop fs:[0]
    pop eax

    invoke MessageBox,NULL,addr szText,NULL,MB_OK
    invoke ExitProcess,NULL
    end start
```

行29～50数据定义了加载配置目录`IMAGE_LOAD_CONFIG_DIRECTORY`结构。第30行设置字段`Characteristics`的值为0x48，第48行定义安全异常函数列表指向第53行，在安全异常函数列表中只定义了函数`_handler1`的地址，其后紧跟着一个双字的0结束。

行103～106代码将函数`_handler1`加入到SEH框架。

行108～109通过赋值指令引发一个异常。按照本章开始部分的描述，该异常会首先跳转到fs:0处注册的SEH异常处理队列的开始处，检查队列中的异常处理函数，看发生的异常是否会被认领。由于在主程序中已经将_handler1函数加入到了SEH框架中，且在PE文件的加载配置目录中声明了该函数为安全的异常处理函数，所以程序引发的异常会被_handler1处理。

编译链接的命令如下：

------

```bash
ml -c -coff exception4.asm
link -subsystem:windows /section: .text,ERW exception4.obj
```

------

## 10.4.2　为PE添加加载配置信息

首先，使用PEInfo小工具查看exception4.exe的所有与节相关的信息如下：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.17iz0tjp4tog.jpg)

因为源代码中将加载配置目录结构放到了代码段的开始，所以，从节的描述中获得代码段在文件的起始偏移为0x00000400，将该地址设置为数据目录表中对加载配置信息的描述里。在数据目录表中添加对IMAGE_LOAD_CONFIG项的定义如下：

![image](https://github.com/YangLuchao/img_host/raw/master/20230921/image.6lpbqz3tjg00.jpg)

## 10.4.3　运行测试

由于加入SEH框架中的异常处理句柄已经加入了PE映像的加载配置信息中，所以，系统认领了该处理函数，并执行。如源代码开始的注释中所说，运行结果最终会出现两个对话框：一个是函数_handler1中代码行69处语句弹出的对话框，还有一个就是执行完异常以后主程序代码行116处的语句弹出的对话框。

再来看另外一个程序exception4_2.asm，入口部分代码如下（详细代码请参照随书文件chapter10\exception4_2.asm）：

------

```assembly
……
start:
assume fs:nothing
push offset _handler2 ;将未注册的异常回调函数加入SEH框架
push fs:[0]
mov fs:[0],esp
xor eax,eax ;引发越界异常
mov dword ptr [eax],eax
……
```

------

该程序与exception4.asm的不同之处在于：加入到SEH框架中的异常处理函数未在PE映像的加载配置信息中定义，所以执行时一个对话框也不会出。

## 10.4.4　注册多个异常处理函数示例

源代码chapter10\exception4_1.asm演示了同时注册多个safeHandler的情况，如代码清单10-3所示。

代码清单10-3　注册多个异常处理函数的示例程序（chapter10\exception4_1.asm）

```assembly
;------------------------
; 测试异常处理
; 与exception4.asm不同之处，注册的异常处理程序为_handler2
; safe Handler列表中两个函数都进行了注册
; 戚利
; 2011.1.19
;------------------------
    .386
    .model flat,stdcall
    option casemap:none

include    windows.inc
include    user32.inc
includelib user32.lib
include    kernel32.inc
includelib kernel32.lib

;数据段
    .data
szText1     db  'safeHandler!',0
szText2     db  'Second safeHandler!',0
szText      db  'HelloWorldPE',0

;代码段
    .code

;IMAGE_LOAD_CONFIG_STRUCT STRUCT
    Characteristics dd                  00000048h
    TimeDateStamp dd                    0
    MajorVersion dw                     0
    MinorVersion dw                     0
    GlobalFlagsClear dd                 0
    GlobalFlagsSet dd                   0
    CriticalSectionDefaultTimeout dd    0
    DeCommitFreeBlockThreshold dd       0
    DeCommitTotalFreeThreshold dd       0
    LockPrefixTable dd                  0
    MaximumAllocationSize dd            0
    VirtualMemoryThreshold dd           0
    ProcessHeapFlags dd                 0
    ProcessAffinityMask dd              0
    CSDVersion dw                       0
    Reserved1 dw                        0
    EditList dd                         0
    SecurityCookie dd                  00000000h
    SEHandlerTable dd                  offset safeHandler ;(VA地址)
    SEHandlerCount  dd                 00000002h
;IMAGE_LOAD_CONFIG_STRUCT ENDS

;构造RVA
safeHandler      dd    offset _handler1-00400000h
                 dd    offset _handler2-00400000h
                 dd    0


;-------------------------------------------
; 已注册的异常回调函数1
;-------------------------------------------
_handler1 proc _lpException,_lpSEH,\
             _lpContext,_lpDispatcherContext
  nop
  pushad
  mov esi,_lpException
  mov edi,_lpContext
   
  assume edi:ptr CONTEXT

  invoke MessageBox,NULL,addr szText1,NULL,MB_OK

  mov [edi].regEip,offset _safePlace
  assume edi:nothing

  popad  

  mov eax,ExceptionContinueExecution
  ret
_handler1 endp

;-------------------------------------------
; 以注册的异常回调函数2
;-------------------------------------------
_handler2 proc _lpException,_lpSEH,\
             _lpContext,_lpDispatcherContext
  nop
  pushad
  mov esi,_lpException
  mov edi,_lpContext
   
  assume edi:ptr CONTEXT

  invoke MessageBox,NULL,addr szText2,NULL,MB_OK

  mov [edi].regEip,offset _safePlace
  assume edi:nothing

  popad  
  mov eax,ExceptionContinueExecution
  ret
_handler2 endp

start:
    assume fs:nothing
    push offset _handler2
    push fs:[0]
    mov fs:[0],esp

    xor eax,eax  ;引发越界异常
    mov dword ptr [eax],eax

_safePlace:

    pop fs:[0]
    pop eax

    invoke MessageBox,NULL,addr szText,NULL,MB_OK
    invoke ExitProcess,NULL
    end start
```

如上所述，==在一个程序中允许注册多个安全的异常处理程序，开发者可以在程序的任何地方捕获异常，并将控制权转由异常处理程序进行处理==。==这使得在大型程序设计中对异常的处理变得高效有序，且能从系统层面杜绝一些不安全的异常接管==。==需要注意的是，在定义多个`safe Handler`的时候，一定要注意对RVA要按照从小到大的顺序进行排序，否则，系统在遍历该表时会出现运行流转错误==。

大家可以使用OD对exception4进行调试。可以从地址7C95411A处下断点（方法是在OD中输入命令BP 7C95411A），从此处开始的代码是对仿真函数RtlIsValidHandler的一个最好的诠释。

> 注意　未经许可，对商业产品进行反编译和反汇编都是违法行为。

# 10.5　小结

本章从Windows中的异常开始，简单介绍了内核模式和用户模式下的SEH处理过程；研究了PE映像中的加载配置信息。特别是针对PE映像中的安全句柄（safe Handler）概念进行了阐述；最后，通过实际的例子演示了PE映像中的加载配置信息是如何参与系统的异常处理机制的。

熟练掌握本章的知识，不仅对理解操作系统结构化异常处理机制有帮助，而且还可以学会通过系统机制实现一些附加的初始化代码的运行，通过接管异常处理实现热补丁等。