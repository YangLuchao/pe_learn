[toc]

第4章介绍了PE文件的导入表，==导入表描述的是其所在PE文件中的指令调用了其他动态链接库函数的情况==。本章重点介绍与导入表刚好相反的==导出表，它描述了导出表所在PE文件向其他程序提供的可供调用的函数的情况==。

==一般情况下，PE中的导出表存在于动态链接库文件里。导出表的主要作用是将PE中存在的函数引出到外部，以便其他人可以使用这些函数，实现代码的重用==。本章将和大家一起制作一个含有导出表的DLL文件，并通过该DLL文件分析导出表结构，讨论导出表的利用技术。

# 5.1　导出表的作用

==代码重用机制提供了重用代码的动态链接库，它会向调用者说明库里的哪些函数是可以被别人使用的，这些用来说明的信息便组成了导出表==。

通常情况下，导出表存在于动态链接库文件里。但我们不能简单地认为EXE中没有导出表，例如WinWord.exe文件里就有；也不能简单地认为所有的DLL中都有导出表，例如一些专门存放资源文件的DLL里就没有导出表。==不过可以这样理解：EXE文件中很少有导出表，大部分的DLL文件中都有导出表。所以一提到导出表，大家总是首先想到动态链接库==。

==导出表的存在可以让程序的开发者很容易清楚PE中到底有多少可以使用的函数，但如果没有函数使用说明，开发者也只能通过名称、反汇编代码或者运行结果对函数的调用方式、函数的功能等进行猜测==。如同Win32 API函数一样，每位开发者可以通过MSDN网站获取对每个API函数的声明、功能介绍、参数介绍，甚至调用实例。

> 提示　如果你想开发一个类库供自己或他人使用，不仅要通过导出机制声明每个库文件的公用函数，还要详细编写每个函数的说明文档。

==Windows装载器在进行PE装载时，会将导入表中登记的所有DLL一并装入，然后根据DLL的导出表中对导入函数的描述修正导入表的IAT值==。==通过导出表，DLL文件向调用它的程序或系统提供导出函数的名称、序号，以及入口地址等信息==。

综上所述，可以得出结论，==导出表的作用有两个==：

1. ==可以通过导出表分析不认识的动态链接库文件所能提供的功能==。
2. ==向调用者提供输出函数指令在模块中的起始地址==。

下面分别进行讨论。

## 5.1.1　分析动态链接库功能

很多时候，我们都得不到某些动态链接库中输出函数的完整说明，这时候只能通过猜测，猜测的依据就是导出表中输出的函数的名字。以下是从一个安装程序中获取到的某动态链接库文件的导出表：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.5qg9yjchg500.webp)

根据输出函数的英文提示不难看出，该动态链接库提供了程序的License验证功能（通过系列函数PSA_GetLicenseXXXXXX可以看出），而且程序具有限制并发连接数功能（通过函数PSA_GetNumberOfConnections可以看出）。对于猜测出功能的函数，可以尝试通过程序调用来测试函数的参数以及调用方法，从而为自己所用。这样不仅避免了重复开发，还大大提高了开发效率。

## 5.1.2　获得导出函数地址

==对一个动态链接库里导出的函数的调用，既可以通过函数名称来进行，也可以通过函数在导出表的索引来进行==。==Windows加载器将与进程相关的DLL加载到虚拟地址空间以后，会根据导入表中登记的与该动态链接库相关的由INT指向的名称或编号来遍历DLL所在虚拟地址空间，通过函数名或编号查找导出表结构，从而确定该导出函数在虚拟地址空间中的起始地址VA，并将该VA覆盖导入表的IAT相关项==。

==在覆盖IAT的过程中，导出表起到了参照和指引的作用==。如果一个动态链接库没有定义导出表，其内部包含的所有函数都无法被其他程序透明地调用。这里所说的透明，是指公开调用。当然，==只要你掌握了动态链接库的内部编码，即使没有导出表，你也可以随意地引用里面的函数，哪怕这些函数是私有的==。5.5.2节将对私有函数的导出方法进行阐述。

# 5.2　构造含导出表的PE文件

本节将编写并创建一个动态链接库文件，该文件输出的函数可以被其他的基于Windows GUI的程序调用，用来增加程序窗口显示或退出时的动态效果。本节的例子是本书第一个以DLL为扩展名的PE文件，它提供了两种窗口显示或退出时可以使用的特效：

- 逐级缩放
- 渐入渐出

使用一个DLL文件需要经过以下四步：

- 步骤1　编写DLL文件的源代码。
- 步骤2　编写函数导出声明文件（扩展名为def）。
- 步骤3　使用特殊参数编译链接生成最终的DLL文件。
- 步骤4　编写包含文件（扩展名为inc）。

在其他源代码中，可以通过静态引用和动态加载两种技术使用新编写的DLL文件中声明的导出函数。下面分别介绍。

## 5.2.1　DLL源代码

编写DLL源代码和编写其他程序唯一不同之处是，在源代码内部必须定义DLL的入口函数。该入口函数必须符合一定的格式。详细代码见代码清单5-1。

代码清单5-1　显示窗口的特殊效果（chapter5\winResult.asm）

------

```assembly
; 设置汇编的版本和模型
.386
.model flat,stdcall
option casemap:none

; 包含所需的 Windows 头文件和库
include    windows.inc
include    user32.inc
includelib user32.lib
include    kernel32.inc
includelib kernel32.lib

; 定义常量
MAX_XYSTEPS equ 50     ; X 和 Y 步骤的最大数量
DELAY_VALUE equ 50     ; 动画效果使用的延迟值
X_STEP_SIZE equ 10     ; X 步骤的大小
Y_STEP_SIZE equ 9      ; Y 步骤的大小
X_START_SIZE equ 20    ; 初始 X 大小
Y_START_SIZE equ 10    ; 初始 Y 大小
LMA_ALPHA equ 2        ; Alpha 混合模式
LMA_COLORKEY equ 1     ; 颜色键混合模式
WS_EX_LAYERED equ 80000h ; 扩展窗口样式，用于创建层叠窗口

; 数据段
.data
dwCount  dd  ?         ; 未使用的数据，可能用于计数
Value    dd  ?         ; 未使用的数据，可能用于存储数值
Xsize    dd  ?         ; 窗口的 X 大小
Ysize    dd  ?         ; 窗口的 Y 大小
sWth     dd  ?         ; 屏幕的宽度
sHth     dd  ?         ; 屏幕的高度
Xplace   dd  ?         ; 窗口的 X 位置
Yplace   dd  ?         ; 窗口的 Y 位置
counts   dd  ?         ; 计数器，用于控制动画步骤
pSLWA    dd  ?         ; SetLayeredWindowAttributes 函数的指针
User32   db  'user32.dll',0   ; 字符串：User32.dll 文件名
SLWA     db  'SetLayeredWindowAttributes',0 ; 字符串：SetLayeredWindowAttributes 函数名

; 代码段
.code
;------------------
; DLL 入口
;------------------
DllEntry   proc  _hInstance,_dwReason,_dwReserved
    mov eax,TRUE
    ret
DllEntry   endp

;-------------------------------
; 私有函数：计算两个数字的平均值
;-------------------------------
TopXY proc wDim:DWORD,sDim:DWORD
     shr sDim,1 
     shr wDim,1
     mov eax,wDim
     sub sDim,eax
     mov eax,sDim
     ret
TopXY endp

;-----------------------------------------------------------
; 窗口抖动进入效果
; 参数：hWin - 要应用动画效果的窗口句柄
;-----------------------------------------------------------
AnimateOpen proc hWin:DWORD
    LOCAL Rct:RECT

    ; 获取窗口的初始位置和大小
    invoke GetWindowRect,hWin,ADDR Rct
    mov Xsize,X_START_SIZE
    mov Ysize,Y_START_SIZE

    ; 获取屏幕的宽度和高度
    invoke GetSystemMetrics,SM_CXSCREEN
    mov sWth,eax
    invoke TopXY,Xsize,eax
    mov Xplace,eax
    invoke GetSystemMetrics,SM_CYSCREEN
    mov sHth,eax
    invoke TopXY,Ysize,eax
    mov Yplace,eax
    mov counts,MAX_XYSTEPS

aniloop:
    ; 移动窗口并显示
    invoke MoveWindow,hWin,Xplace,Yplace,Xsize,Ysize,FALSE
    invoke ShowWindow,hWin,SW_SHOWNA

    ; 等待一段时间
    invoke Sleep,DELAY_VALUE

    ; 隐藏窗口
    invoke ShowWindow,hWin,SW_HIDE

    ; 增加窗口大小
    add Xsize,X_STEP_SIZE
    add Ysize,Y_STEP_SIZE

    ; 计算新的窗口位置
    invoke TopXY,Xsize,sWth
    mov Xplace,eax
    invoke TopXY,Ysize,sHth
    mov Yplace,eax

    ; 减少计数器
    dec counts
    jnz aniloop

    ; 计算新的窗口大小
    mov eax,Rct.left
    mov ecx,Rct.right
    sub ecx,eax
    mov Xsize,ecx
    mov eax,Rct.top
    mov ecx,Rct.bottom
    sub ecx,eax
    mov Ysize,ecx

    ; 计算新的窗口位置
    invoke TopXY,Xsize,sWth
    mov Xplace,eax
    invoke TopXY,Ysize,sHth
    mov Yplace,eax

    ; 移动窗口并显示
    invoke MoveWindow,hWin,Xplace,Yplace,Xsize,Ysize,TRUE 
    invoke ShowWindow,hWin,SW_SHOW
    ret 
AnimateOpen endp

;-------------------------
; 窗口抖动退出效果
; 参数：hWin - 要应用动画效果的窗口句柄
;-------------------------
AnimateClose proc hWin:DWORD
    LOCAL Rct:RECT

    ; 隐藏窗口
    invoke ShowWindow,hWin,SW_HIDE

    ; 获取窗口的初始位置和大小
    invoke GetWindowRect,hWin,ADDR Rct

    ; 计算初始窗口大小
    mov eax,Rct.left
    mov ecx,Rct.right
    sub ecx,eax
    mov Xsize,ecx
    mov eax,Rct.top
    mov ecx,Rct.bottom
    sub ecx,eax
    mov Ysize,ecx

    ; 获取屏幕的宽度和高度
    invoke GetSystemMetrics,SM_CXSCREEN
    mov sWth,eax
    invoke TopXY,Xsize,eax
    mov Xplace,eax
    invoke GetSystemMetrics,SM_CYSCREEN
    mov sHth,eax
    invoke TopXY,Ysize,eax
    mov Yplace,eax
    mov counts,MAX_XYSTEPS

aniloop:
    ; 移动窗口并显示
    invoke MoveWindow,hWin,Xplace,Yplace,Xsize,Ysize,FALSE 
    invoke ShowWindow,hWin,SW_SHOWNA

    ; 等待一段时间
    invoke Sleep,DELAY_VALUE

    ; 隐藏窗口
    invoke ShowWindow,hWin,SW_HIDE

    ; 减小窗口大小
    sub Xsize,X_STEP_SIZE
    sub Ysize,Y_STEP_SIZE

    ; 计算新的窗口位置
    invoke TopXY,Xsize,sWth
    mov Xplace,eax
    invoke TopXY,Ysize,sHth
    mov Yplace,eax

    ; 减少计数器
    dec counts
    jnz aniloop

    ret
AnimateClose endp

;--------------------------------------------
; 窗口淡入效果，仅运行在2000/XP以上操作系统
; 参数：hWin - 要应用动画效果的窗口句柄
;--------------------------------------------
FadeInOpen proc hWin:DWORD

    ; 设置窗口样式为 WS_EX_LAYERED
    invoke GetWindowLongA,hWin,GWL_EXSTYLE
    or eax,WS_EX_LAYERED
    invoke SetWindowLongA,hWin,GWL_EXSTYLE,eax

    ; 获取 SetLayeredWindowAttributes 函数的地址
    invoke GetModuleHandleA,ADDR User32
    invoke GetProcAddress,eax,ADDR SLWA
    mov pSLWA,eax

    ; 设置窗口为透明，开始淡入效果
    push LMA_ALPHA
    push 0 
    push 0
    push hWin
    call pSLWA

    ; 初始化计数器和透明度
    mov Value,90

    ; 显示窗口
    invoke ShowWindow,hWin,SW_SHOWNA

doloop:
    ; 逐渐增加窗口透明度
    push LMA_COLORKEY + LMA_ALPHA
    push Value
    push Value
    push hWin
    call pSLWA

    ; 等待一段时间
    invoke Sleep,DELAY_VALUE

    ; 增加透明度值
    add Value,15

    ; 如果透明度未达到最大值 (255)，则继续循环
    cmp Value,255
    jne doloop

    ; 恢复透明度为不透明
    push LMA_ALPHA
    push 255
    push 0
    push hWin
    call pSLWA

    ret 
FadeInOpen endp

;--------------------------------------------
; 窗口淡出效果，仅运行在2000/XP以上操作系统
; 参数：hWin - 要应用动画效果的窗口句柄
;--------------------------------------------
FadeOutClose proc hWin:DWORD

    ; 设置窗口样式为 WS_EX_LAYERED
    invoke GetWindowLongA,hWin,GWL_EXSTYLE
    or eax,WS_EX_LAYERED
    invoke SetWindowLongA,hWin,GWL_EXSTYLE,eax

    ; 获取 SetLayeredWindowAttributes 函数的地址
    invoke GetModuleHandleA,ADDR User32
    invoke GetProcAddress,eax,ADDR SLWA
    mov pSLWA,eax

    ; 设置窗口为完全不透明，开始淡出效果
    push LMA_ALPHA
    push 255
    push 0
    push hWin
    call pSLWA

    ; 初始化透明度为完全不透明 (255)
    mov Value,255

doloop:
    ; 逐渐降低窗口透明度
    push LMA_COLORKEY + LMA_ALPHA
    push Value
    push Value
    push hWin
    call pSLWA

    ; 等待一段时间
    invoke Sleep,DELAY_VALUE

    ; 减少透明度值
    sub Value,15

    ; 如果透明度未达到最小值 (0)，则继续循环
    cmp Value,0
    jne doloop

    ret
FadeOutClose endp

; DLL 的结束标记
End DllEntry

```

------

以上代码中一共定义了6个函数，其中有4个是可以被导出的公有函数，有一个私有函数，还有一个动态链接库必须具备的入口函数。4个公有函数分别代表了4种显示窗口的动态效果：

- AnimateOpen（窗口放大进入）
- AnimateClose（窗口缩小退出）
- FadeInOpen（窗口淡入）
- FadeOutClose（窗口淡出）

入口函数名为DllEntry，由于我们没有初始化代码，所以只是简单地返回了TRUE。入口函数负责处理动态链接库的生命周期中发生的各种消息，如库被装载、卸载、新线程创建和新线程结束等操作。入口函数的名字可以随意命名，但格式必须遵循一定规范（这些规范包括入口参数的个数和返回值的类型）。

## 5.2.2　编写def文件

为了能让系统识别哪些是私有函数，哪些是导出函数，还需要在源代码外另外附加一个文件，在文件中列出要导出的函数的名字。这个文件就是def文件。==链接器会根据这个def文件的内容在导出表中加入由EXPORTS指定的函数名==。以下是编写def文件的过程：在记事本中输入如下内容，并保存为文件"winResult.def"。

------

```assembly
EXPORTS AnimateOpen
	AnimateClose
	FadeInOpen
	FadeOutClose
```

------

## 5.2.3　编译和链接

编译的方法和前面其他章节的编译方法没有区别，链接时需要额外增加两个链接参数，命令如下：

------

```bash
D:\masm32\source\chapter6＞ml -c -coff winResult.asm
D:\masm32\source\chapter6＞link -DLL -subsystem:windows -Def:winResult.def winResult.obj
```

------

==新增加的两个链接参数分别是"-DLL"和"-Def"。前者表示生成的最终文件是一个动态链接库，扩展名为dll；后者表示在生成的链接库的导出表中，加入该参数指定的def文件中的函数。==

链接后生成以下两个相关文件：

- ==winResult.dll是动态链接库文件==。该文件可以共享给使用其他高级语言如VC++、Delphi、VB等的开发者使用。
- ==winResult.lib是汇编语言环境下的库文件==。使用winResult.dll的汇编程序必须使用该库文件和下面要介绍的inc包含文件。

## 5.2.4　编写头文件

包含文件扩展名为".inc"，该文件类似于C语言的".h"头文件，所以又称为头文件。该文件中包含了动态链接库中导出函数的声明。将如下内容输入记事本程序，并保存为"winResult.inc"。

------

```assembly
AnimateOpen proto :dword
AnimateClose proto :dword
FadeInOpen proto :dword
FadeOutClose proto :dword
```

------

==有了动态链接库、相关的lib文件和头文件，就可以在任何一个程序的代码中使用这个DLL里导出的函数了==。

## 5.2.5　使用导出函数

接下来使用刚生成的三个文件：winResult.dll、winResult.lib和winResult.inc来编写一个渐入式窗口显示程序，详细代码见清单5-2。

代码清单5-2具备动态显示效果的窗口程序（chapter5\FirstWindow.asm）

------

```assembly
;------------------------
; 简单窗口程序
; 具有窗口的大部分基本特性，其中显示和退出使用了渐入和渐出效果
; 该程序主要演示自己制作的dll的函数调用
; 戚利
; 2010.6.27
;------------------------

.386
.model flat,stdcall
option casemap:none

include    windows.inc
include    gdi32.inc
includelib gdi32.lib
include    user32.inc
includelib user32.lib
include    kernel32.inc
includelib kernel32.lib
include    winResult.inc
includelib winResult.lib

; 数据段
.data?
hInstance  dd  ?     ; 存储应用程序实例句柄
hWinMain   dd  ?     ; 存储主窗口句柄

; 常量定义
.const
szClassName    db  'MyClass',0         ; 窗口类名
szCaptionMain  db  '窗口特效演示',0     ; 主窗口标题
szText         db  '你好，认识我吗？^_^',0  ; 显示在窗口内的文本

; 代码段
.code

;------------------
; 窗口消息处理子程序
;------------------
_ProcWinMain proc uses ebx edi esi,hWnd,uMsg,wParam,lParam
      local @stPs:PAINTSTRUCT
      local @stRect:RECT
      local @hDc

      mov eax,uMsg

      .if eax==WM_PAINT
          invoke BeginPaint,hWnd,addr @stPs
          mov @hDc,eax
          invoke GetClientRect,hWnd,addr @stRect
          invoke DrawText,@hDc,addr szText,-1,\
                 addr @stRect,\
                 DT_SINGLELINE or DT_CENTER or DT_VCENTER
          invoke EndPaint,hWnd,addr @stPs
      .elseif eax==WM_CLOSE    ; 关闭窗口
             invoke FadeOutClose,hWinMain
             invoke DestroyWindow,hWinMain
             invoke PostQuitMessage,NULL
      .else
          invoke DefWindowProc,hWnd,uMsg,wParam,lParam
          ret
      .endif
      
      xor eax,eax
      ret
_ProcWinMain endp

;----------------------
; 主窗口程序
;----------------------
_WinMain  proc
       local @stWndClass:WNDCLASSEX
       local @stMsg:MSG

       invoke GetModuleHandle,NULL
       mov hInstance,eax
       invoke RtlZeroMemory,addr @stWndClass,sizeof @stWndClass
       
       ; 注册窗口类
       invoke LoadCursor,0,IDC_ARROW
       mov @stWndClass.hCursor,eax
       push hInstance
       pop @stWndClass.hInstance
       mov @stWndClass.cbSize,sizeof WNDCLASSEX
       mov @stWndClass.style,CS_HREDRAW or CS_VREDRAW
       mov @stWndClass.lpfnWndProc,offset _ProcWinMain
       mov @stWndClass.hbrBackground,COLOR_WINDOW+1
       mov @stWndClass.lpszClassName,offset szClassName
       invoke RegisterClassEx,addr @stWndClass

       ; 建立并显示窗口
       invoke CreateWindowEx,WS_EX_CLIENTEDGE,\
              offset szClassName,offset szCaptionMain,\
              WS_OVERLAPPEDWINDOW,\
              100,100,600,400,\
              NULL,NULL,hInstance,NULL
       mov  hWinMain,eax
       invoke FadeInOpen,hWinMain
       ;invoke ShowWindow,hWinMain,SW_SHOWNORMAL
       invoke UpdateWindow,hWinMain ; 更新客户区，即发送WM_PAINT消息

   
       ; 消息循环
       .while TRUE
          invoke GetMessage,addr @stMsg,NULL,0,0
          .break .if eax==0
          invoke TranslateMessage,addr @stMsg
          invoke DispatchMessage,addr @stMsg
       .endw
       ret
_WinMain endp

start:
       call _WinMain
       invoke ExitProcess,NULL
       end start

```

------

与引入其他动态链接库的方法一样，在代码行19引入winResult.lib，在行20引入头文件winResult.inc。创建窗口以后，显示时不再使用如下所示的正常的显示代码：

------

```assembly
invoke ShowWindow,hWinMain,SW_SHOWNORMAL
```

------

而是使用了具有动画特效的代码（如下所示），该代码由动态链接库winResult.dll输出。

------

```assembly
invoke FadeInOpen,hWinMain
```

------

可以看出，调用公用函数的方法和调用其他动态链接库的方法（使用常规方法编译链接程序，生成FirstWindow.exe，然后运行它以便查看窗口显示的动画效果）是完全一样的。

接下来简单分析到目前为止接触的两种不同类型的PE文件：

- FirstWindow.exe（EXE可执行文件）
- winResult.dll（DLL动态链接库）

使用PEInfo小工具查看两者结构，通过比较可以看出，两种不同类型的PE文件在内容上存在很大差异，如头文件中的装载基地址、入口地址、文件属性均不相同。EXE文件的PE格式中不存在重定位表项，也没有导出表；而DLL文件的PE格式中这两项都存在。

# 5.3　导出表数据结构

本节将描述PE文件中的导出表及其数据组织方式。内容包括：

- 在PE文件里定位导出表
- 导出表的数据组织结构
- 导出表实例

下面分别来介绍。

## 5.3.1　导出表定位

导出表数据为数据目录中注册的数据类型之一，其描述信息处于数据目录的第1个目录项中。使用PEDump小工具获取chapter5\winResult.dll的数据目录内容如下：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.48c5zk4uxos0.webp)

加黑部分为导出表数据目录项信息。通过以上字节码可以获得与导出表有关的两条信息：

- 导出表所在地址RVA=0x000002140
- 导出表数据大小=0000008fh

以下是使用小工具PEInfo获取的该文件所有的节信息：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.5syv350udj40.webp)

根据RVA与FOA的换算关系，可以得到：

> 计算过程如下：
>
> RVA = 0x00002140
>
> RVA0=0x00002000
>
> Dif = RVA - RVA0 = 0x140
>
> FOV = rdata + dif = 800+140 = 940

导出表数据所在文件的偏移地址为：0x00000940。

## 5.3.2　导出目录IMAGE_EXPORT_DIRECTORY

导出数据的第一个结构是IMAGE_EXPORT_DIRECTORY。该结构详细定义如下：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.6990plhff0o0.webp)

导入表的IMAGE_IMPORT_DESCRIPTOR个数与调用的动态链接库个数相等，而导出表的IMAGE_EXPORT_DIRECTORY只有一个。各字段解释如下：

#### 64.IMAGE_EXPORT_DIRECTORY.nName

+000ch，双字。==该字段指示的地址指向了一个以“\0”结尾的字符串，字符串记录了导出表所在的文件的最初文件名==。

#### 65.IMAGE_EXPORT_DIRECTORY.NumberOfFunctions

+0014h，双字。==该字段定义了文件中导出函数的总个数==。

#### 66.IMAGE_EXPORT_DIRECTORY.NumberOfNames

+0018h，双字。==在导出表中，有些函数是定义名字的，有些是没有定义名字的==。==该字段记录了所有定义名字函数的个数==。如果这个值是0，则表示所有的函数都没有定义名字。NumberOfNames和NumberOfFunctions的关系是前者小于等于后者。

#### 67.IMAGE_EXPORT_DIRECTORY.AddressOfFunctions

+001ch，双字。==该指针指向了全部导出函数的入口地址的起始==。从入口地址开始为双字数组，数组的个数由字段IMAGE_EXPORT_DIRECTORY.NumberOfFunctions决定。==导出函数的每一个地址按函数的编号顺序依次往后排开==。==在内存中，我们可以通过函数编号来定位某个函数的地址==。大致代码如下：

------

```assembly
mov eax,[esi].AddressOfFunctions ;esi指向导出表结构IMAGE_EXPORT_DIRECTORY的起始地址
mov ebx,num ;假设ebx为函数编号
sub ebx,[esi].nBase
add eax,[num*4]
mov eax,[eax] ;到这里已获取函数的虚拟地址RVA，加上模块实际装入地址,就是在虚拟地址空间里真实的地址VA
```

------

#### 68.IMAGE_EXPORT_DIRECTORY.nBase

+0010h，双字。==导出函数编号的起始值==。==DLL中的第一个导出函数并不是从0开始的，某导出函数的编号等于从AddressOfFunctions开始的顺序号加上这个值==，大致示意图如图5-1所示。

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.gto2qcdg0pk.webp)

图　5-1　nBase字段决定导出函数编号

如图所示，Fun1的函数编号为nBase+0=200h，Fun2的函数编号为nBase+1=201h，以此类推。

#### 69.IMAGE_EXPORT_DIRECTORY.AddressOfNames

+0020h，双字。该值为一个指针。==该指针指向的位置是一连串的双字值==，==这些双字值均指向了对应的定义了函数名的函数的字符串地址==。这一连串的双字个数为NumberOf Names。

#### 70.IMAGE_EXPORT_DIRECTORY.AddressOfNameOrdinals

+0024h，双字。该值也是一个指针，==与AddressOfNames是一一对应关系（注意，是一一对应），所不同的是，AddressOfNames指向的是字符串的指针数组，而AddressOfNameOrdinals则指向了该函数在AddressOfFunctions中的索引值==。

> 注意　索引值是一个字，而非双字。该值与函数编号是两个不同的概念，两者之间的关系为：
>
> `索引值 = 编号 - nBase`

以上所述，字段之间的关系可以用图5-2表示。

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.hstj9paure0.webp)

图　5-2　PE导出表结构

如图所示，==AddressOfNames中的函数是从Function2开始的，也就是说，这里假设Function1只提供编号访问==；==其nBase为200h，所以对应的AddressOfNameOrdinals是0001h，但最终函数Function1的编号为：索引值 + nBase的值，即0201h==。

> 提示　在图4-5中最后指向的结构"Hint/Name"中的==Hint值是AddressOfFunctions的索引值，并非函数编号==。

## 5.3.3　导出表实例分析

下面以动态链接库winResult.dll为例，分析该PE文件的导出表结构及其数据组织。

使用小工具PEDump获取winResult.dll的导出表字节码内容如下（从文件地址偏移0x00000940开始）：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.6nyyb45opn00.webp)

其中，主要字段所对应的字节码解释如下：

------

```assembly
＞＞ 90 21 00 00
```

------

对应IMAGE_EXPORT_DIRECTORY.nName字段，指向文件偏移0x00000990。该处的值为字符串"winResult.dll"，是动态链接库的最初的名字。

------

```assembly
＞＞ 01 00 00 00
```

------

对应IMAGE_EXPORT_DIRECTORY.nBase字段，表示起始编号为1。

------

```assembly
＞＞ 04 00 00 00
```

------

对应IMAGE_EXPORT_DIRECTORY.NumberOfFunctions字段，表示共有4个导出函数。

------

```assembly
＞＞ 04 00 00 00
```

------

==对应IMAGE_EXPORT_DIRECTORY.NumberOfNames字段，表示4个导出函数均为按照名称导出==。

------

```assembly
＞＞ 68 21 00 00
```

------

对应IMAGE_EXPORT_DIRECTORY.AddressOfFunctions字段。从该位置取出连续4个地址（个数由IMAGE_EXPORT_DIRECTORY.NumberOfFunctions字段决定），这些地址分别对应4个函数的RVA。AddressOfFunctions的值见图5-3。

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.53hysvla96k0.webp)

图　5-3　AddressOfFunctions数组

------

```assembly
＞＞ 78 21 00 00
```

------

对应IMAGE_EXPORT_DIRECTORY.AddressOfNames字段。从该位置取出的连续4个地址依次为：

![image](https://github.com/YangLuchao/img_host/raw/master/20231007/image.j1px5sa1chc.jpg)

------

```assembly
＞＞ 88 21 00 00
```

------

对应IMAGE_EXPORT_DIRECTORY.AddressOfNameOrdinals字段。从该位置取出的连续4个单字索引依次为：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.51a41p0wasg0.webp)

这些索引的值存在于字段IMAGE_EXPORT_DIRECTORY.AddressOfFunctions所指向的函数地址列表中。最终4个函数的编号将分别是此处的索引值加上nBase的值，即0001、0002、0003和0004。函数名对应的索引值可以在调用了该动态链接库的程序FirstWindow.exe的导入表数据中查找到（以下加黑部分）：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.4dfsuzuv0te0.webp)

# 5.4　导出表编程

本节讨论与导出表有关的编程，==包括获取导出表中导出函数地址的编程，以及遍历导出表信息的编程两部分==。

如前所述，通过导出表可以获取相关函数的地址。==函数可以通过索引值定位，也可以通过函数名定位==。通过编程查找函数地址有两个不同方法，分别是：

- 根据编号查找函数地址
- 根据名字查找函数地址

下面分别介绍这两种方法。

## 5.4.1　根据编号查找函数地址

要通过编号查找函数地址，其步骤如下：

1. 步骤1　定位到PE头。
2. 步骤2　从PE文件头中找到数据目录表，表项的第一个双字值是导出表的起始RVA。
3. 步骤3　从导出表的nBase字段得到起始序号。
4. 步骤4　函数编号减去起始序号得到的是函数在AddressOfFunctions中的索引号。
5. 步骤5　通过查询AddressOfFunctions指定索引位置的值，找到虚拟地址。
6. 步骤6　将虚拟地址加上该动态链接库在被导入到地址空间后的基地址，即为函数的真实入口地址。

不建议使用编号查找函数地址。因为有很多的动态链接库中标识的编号与对应的函数并不一致，通过这种方法找到的函数地址往往是错误的。

## 5.4.2　根据名字查找函数地址

要根据函数名字从导出表结构中查找函数的地址，步骤如下：

1. 步骤1　定位到PE头。
2. 步骤2　从PE文件头中找到数据目录表，表项的第一个双字值是导出表的起始RVA。
3. 步骤3　从导出表中获取NumberOfNames字段的值，以便构造一个循环，根据此值确定循环的次数。
4. 步骤4　从AddressOfNames字段指向的函数名称数组的第一项开始，与给定的函数名字进行匹配；如果匹配成功，则记录从AddressOfNames开始的索引号。
5. 步骤5　通过索引号再去检索AddressOfNameOrdinals数组，从同样索引的位置找到函数的地址索引。
6. 步骤6　通过查询AddressOfFunctions指定函数地址索引位置的值，找到虚拟地址。
7. 步骤7　将虚拟地址加上该动态链接库在被导入到地址空间的基地址，即为函数的真实入口地址。

其中通过函数名获取函数调用地址的编码见代码清单5-3。

代码清单5-3　获取指定字符串的API函数的调用地址的函数`_getApi`（chapter5\peinfo.asm）

------

```assembly
;-------------------------------
; 获取指定字符串的API函数的调用地址
; 入口参数：_hModule为动态链接库的基址，_lpApi为API函数名的首址
; 出口参数：eax为函数在虚拟地址空间中的真实地址
;-------------------------------
_getApi proc _hModule,_lpApi
   local @ret
   local @dwLen

   pushad
   mov @ret,0
   ;计算API字符串的长度，含最后的零
   mov edi,_lpApi
   mov ecx,-1
   xor al,al
   cld
   repnz scasb
   mov ecx,edi
   sub ecx,_lpApi
   mov @dwLen,ecx

   ;从pe文件头的数据目录获取导出表地址
   mov esi,_hModule
   add esi,[esi+3ch]
   assume esi:ptr IMAGE_NT_HEADERS
   mov esi,[esi].OptionalHeader.DataDirectory.VirtualAddress
   add esi,_hModule
   assume esi:ptr IMAGE_EXPORT_DIRECTORY

   ;查找符合名称的导出函数名
   mov ebx,[esi].AddressOfNames
   add ebx,_hModule
   xor edx,edx
   .repeat
     push esi
     mov edi,[ebx]
     add edi,_hModule
     mov esi,_lpApi
     mov ecx,@dwLen
     repz cmpsb
     .if ZERO?
       pop esi
       jmp @F
     .endif
     pop esi
     add ebx,4
     inc edx
   .until edx>=[esi].NumberOfNames
   jmp _ret
@@:
   ;通过API名称索引获取序号索引再获取地址索引
   sub ebx,[esi].AddressOfNames
   sub ebx,_hModule
   shr ebx,1
   add ebx,[esi].AddressOfNameOrdinals
   add ebx,_hModule
   movzx eax,word ptr [ebx]
   shl eax,2
   add eax,[esi].AddressOfFunctions
   add eax,_hModule
   
   ;从地址表得到导出函数的地址
   mov eax,[eax]
   add eax,_hModule
   mov @ret,eax

_ret:
   assume esi:nothing
   popad
   mov eax,@ret
   ret
_getApi endp
```

------

23～24行对应步骤当中的步骤1，定位PE头。

34～49行是一个循环，结束条件为找到对应的函数地址或者函数个数已经达到NumberOfNames字段所标识的值。如何判断函数已经找到了呢？方法是通过与AddressOfNames所列的每个函数名进行比对，如果字符串相等，则表示找到，否则继续下一次循环。如果找到，则跳出循环，转到行50处继续执行。

如果函数名匹配成功，表示找到了对应的函数。则记录AddressOfNames的索引，即步骤4，对应代码中的51～54行。

代码中的55～61行实现了步骤5的操作。步骤6、7则对应63～64行。

## 5.4.3　遍历导出表

遍历导出表的编程是以第4章的PEInfo.asm程序为模板开始的。在函数_openFile中加入以下代码（加黑部分）：

------

```assembly
;到此为止，该文件的验证已经完成。为PE结构文件
;接下来分析文件映射到内存中的数据，并显示主要参数
invoke _getMainInfo,@lpMemory,esi,@dwFileSize
;显示导入表
invoke _getImportInfo,@lpMemory,esi,@dwFileSize
;显示导出表
invoke _getExportInfo,@lpMemory,esi,@dwFileSize
```

------

然后编写函数_getExportInfo，如代码清单5-4所示。

代码清单5-4　遍历导出表的函数_getExportInfo（chapter5\peinfo.asm）

------

```assembly
;--------------------
; 获取PE文件的导出表
;--------------------
_getExportInfo proc _lpFile,_lpPeHead,_dwSize
  local @szBuffer[1024]:byte
  local @szSectionName[16]:byte
  local @lpAddressOfNames,@dwIndex,@lpAddressOfNameOrdinals
  
  pushad
  mov esi,_lpPeHead
  assume esi:ptr IMAGE_NT_HEADERS
  mov eax,[esi].OptionalHeader.DataDirectory[0].VirtualAddress
  .if !eax
    invoke _appendInfo,addr szErrNoExport
    jmp _Ret
  .endif
  invoke _RVAToOffset,_lpFile,eax
  add eax,_lpFile
  mov edi,eax     ;计算导出表所在文件偏移位置
  assume edi:ptr IMAGE_EXPORT_DIRECTORY
  invoke _RVAToOffset,_lpFile,[edi].nName
  add eax,_lpFile
  mov ecx,eax
  invoke _getRVASectionName,_lpFile,[edi].nName
  invoke wsprintf,addr @szBuffer,addr szMsgExport,\
         eax,ecx,[edi].nBase,[edi].NumberOfFunctions,\
         [edi].NumberOfNames,[edi].AddressOfFunctions,\
         [edi].AddressOfNames,[edi].AddressOfNameOrdinals
  invoke _appendInfo,addr @szBuffer

  invoke _RVAToOffset,_lpFile,[edi].AddressOfNames
  add eax,_lpFile
  mov @lpAddressOfNames,eax
  invoke _RVAToOffset,_lpFile,[edi].AddressOfNameOrdinals
  add eax,_lpFile
  mov @lpAddressOfNameOrdinals,eax
  invoke _RVAToOffset,_lpFile,[edi].AddressOfFunctions
  add eax,_lpFile
  mov esi,eax   ;函数的地址表

  mov ecx,[edi].NumberOfFunctions
  mov @dwIndex,0
@@:
  pushad
  mov eax,@dwIndex
  push edi
  mov ecx,[edi].NumberOfNames
  cld
  mov edi,@lpAddressOfNameOrdinals
  repnz scasw
  .if ZERO?  ;找到函数名称
    sub edi,@lpAddressOfNameOrdinals
    sub edi,2
    shl edi,1
    add edi,@lpAddressOfNames
    invoke _RVAToOffset,_lpFile,dword ptr [edi]
    add eax,_lpFile
  .else
    mov eax,offset szExportByOrd
  .endif
  pop edi
  ;序号在ecx中
  mov ecx,@dwIndex
  add ecx,[edi].nBase
  invoke wsprintf,addr @szBuffer,addr szMsg4,\
         ecx,dword ptr [esi],eax
  invoke _appendInfo,addr @szBuffer
  popad
  add esi,4
  inc @dwIndex
  loop @B
_Ret:
  assume esi:nothing
  assume edi:nothing
  popad
  ret
_getExportInfo endp
```

------

行12～16通过检索数据目录表的第1项（用[esi].OptionalHeader.DataDirectory[0]来表示），获取导出表的VirtualAddress。如果该值为0，意味着该PE文件没有导出表，则显示没有导出表的提示信息并退出，否则继续。

行17～30部分，首先将获取的导出表的VirtualAddress的RVA值转换为FOA，然后显示该地址所处的节的名称，并显示结构IMAGE_EXPORT_DIRECTORY的部分字段的值。

行43～71为一个循环，该循环完成了显示该PE文件中所有导出函数相关信息的功能。这些信息包括导出序号、函数的虚拟地址和导出函数的名称。变量@dwIndex跟随循环次数加1递增，同时该变量也是所有函数的索引值。通过查找AddressOfNameOrdinals数组获得该索引是否在数组中存在，以确定对应索引的函数是否是基于名称访问的。如果是，则执行第52～57行的代码；如果不是，表示该函数是基于索引值访问的，则执行第59行的代码。

以下内容是使用PEInfo小工具分析chapter5\winResult.dll文件输出的与导出表有关的信息：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.1g1l8bonaklc.webp)

显示信息分三部分：导出表所处的节、导出表结构IMAGE_EXPORT_DIRECTORY的主要字段的值和导出函数的相关信息（含导出序号、虚拟地址和导出函数名称）。

导出表的结构就分析到这里，下面来看导出表的常见应用。

# 5.5　导出表的应用

==导出表常见的应用主要包括对导出表函数的覆盖，以及对动态链接库内部私有函数的导出等==。==通过对导出表函数进行覆盖，可以更改代码流程或代码功能，为应用程序实施补丁；通过对动态链接库内部私有函数的导出，可以更充分地利用已有的代码，减轻二次开发的工作量。==

## 5.5.1　导出函数覆盖

==导出表编程中常见的技术是，不需要修改用户程序，便能将用户程序中调用的动态链接库函数转向或者实施代码覆盖，实现用户程序的调用转移==。这种技术通常用在病毒程序的开发中，因为用户程序没有发生改变，所以杀毒软件在对用户程序的防护过程中，针对这种渗透是无效的。下面介绍两种常见的导出函数覆盖技术：

1. 修改导出结构中的函数地址
2. 覆盖函数地址部分的指令代码

### 1.修改导出结构中的函数地址

以winResult.dll为例，使用FlexHex将AddressOfFunctions索引1和2的地址（分别对应函数AnimateOpen和FadeInOpen）交换位置。更改以后的字节码如下：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.47gglckcodg0.webp)

如上所示，无需修改应用程序FirstWindow.exe，仅通过将函数调用RVA地址0x00001282和0x00001022交换位置，即可实现导出函数的覆盖。直接测试，发现显示窗口的动画效果发生了变化。

> 注意，==在使用导出函数地址覆盖技术的时候，首先要保证所涉及的两个函数参数入口要一致，否则调用完成后栈不平衡，这会导致应用程序调用失败==；其次，要求用户对两个函数的内部实现要有充分的了解，使得地址转向后，能够保证应用程序在功能上可以全面兼容并运行良好。
>

> 注意　该部分测试文件在随书文件的目录chapter5\a中，winResult.dll是被修改了AddressOfFunctions地址后的动态链接库。在实际的操作中并不赞成大家使用该技术。

### 2.覆盖函数地址部分的指令代码

==第二种常见的覆盖技术，是将AddressOfFunctions指向的地址空间指令字节码实施覆盖==。这种技术又衍生出两种：

1. ==暴力覆盖，即将所有的代码全部替换为新代码。新代码可能含有原来代码的全部功能，也可能不包含原有代码功能==。
2. ==完美覆盖，通过构造指令，实施新代码与原代码的共存和无遗漏运行==。

因为完美覆盖涉及代码的重定位，相对复杂一些，这里以暴力覆盖为例。相关文件在随书文件的chapter5\b目录中，winResult.dll是被覆盖了函数FadeInOpen后的动态链接库。

> 注意
>
> 修改前：
>
> ![image](https://github.com/YangLuchao/img_host/raw/master/20231011/image.6mgejkfl9gw0.jpg)
>
> 修改后：
>
> ![image](https://github.com/YangLuchao/img_host/raw/master/20231011/image.47pbm7zm8mo0.jpg)

打开winResult.dll文件查看字节码，可以发现函数FadeInOpen定义部分（文件中起始偏移0x0682）被修改成如下指令序列：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.3k42jjusfpu0.webp)

为方便阅读，==以上按照指令功能对字节码作了加黑处理==。==第一部分黑体保存原始栈基地址==，==第二部分黑体代码是维持栈平衡的返回指令==。所有字节码对应的反汇编指令为：

> 注意
>
> 修改前
>
> ![image](https://github.com/YangLuchao/img_host/raw/master/20231011/image.2ccg6ru1bn6s.jpg)
>
> 修改后（在这里OD错误地将它识别成了指令序列）：
>
> ![image](https://github.com/YangLuchao/img_host/raw/master/20231011/image.3dfitxx7emg0.jpg)

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.16shhw8qtl6o.webp)

因为函数FadeInOpen的代码被全部覆盖，所以运行FirstWindow只会弹出提示对话框，内容显示"user32.dll"。显示的字符串是借用了winResult.asm的数据段中定义的函数SetLayeredWindowAttributes所在动态链接库的名称。

从反汇编指令可以看出，调用函数user32.MessageBoxA时使用了硬编码，即将该函数在虚拟地址空间分配的VA直接写入了代码段，不通过导入表直接跳转到函数代码处执行，上面反汇编代码加黑部分即为地址字节码（==在这里OD错误地将它识别成了指令序列==）。==使用硬编码最大的好处是引入动态链接库的函数时不需要修改导入表。因为FirstWindow引入的动态链接库就这一个，不存在基地址被占用的问题，所以，与重定位有关的信息在此例中不需要进行修改==。

> 注意 使用暴力覆盖是直接修改程序的硬编码，要求硬编码替换代码应小于被替换代码，空缺字节用NOP填充

## 5.5.2　导出私有函数

==在某些场合下，DLL中的私有函数还是很有用的。也许是出于保密考虑，或者其他原因，DLL的开发者将一些比较重要的函数设置为内部私有函数，并不在导出表中声明。当程序被二次开发时，开发者却需要这些函数，这时候就需要开发者自己将这些被定义为私有的函数添加到导出表中==。

在本章的实例中，程序winResult.dll一共导出了4个公有函数；源代码中的TopXY函数被声明为私有函数，并未导出，所以在使用PEInfo分析时看不到该函数。下面就以这个函数为例，介绍一下导出私有函数需要做哪些工作。

首先，将最原始的导出表整体搬迁到一个空闲空间中，这里选择从文件偏移0x0940处搬到0x0a50处。以下是添加私有函数到导出表后的两处地址对应的字节码：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.5l2w5rys21s0.webp)

现在从几个方面来分析添加了私有函数的导出表与原有导出表的区别：

##### 1）长度从8Fh变成了9Fh。增加的部分包含：

函数名：'TopXY\0'共6个字节。

函数的RVA：0x0000100c，共4个字节。

函数名称所在地址：0x000032e9，共4个字节。

##### 2）函数个数由原来的4个变成5个（见字节码的下划线部分）。

##### 3）修正其他因搬迁和增加而变动的地址。

因为上面已经同时列出了前后的字节码，在这里就不再详细描述，大家可以比照两者不同和导出表结构自己进行分析。

##### 4）数据目录项。因为导出表位置和数据大小发生了变化，所以PE文件头部的数据目录项中需要进行如下修正：

位置由原来的0x2140变成0x3250。

大小由原来的8Fh变成9Fh。

这样就为导出表增加了函数TopXY的导出信息。使用小工具PEInfo查看导出表，输出信息如下：

![image](https://github.com/YangLuchao/img_host/raw/master/20230911/image.re8cv9imm80.webp)

如上所示，加黑部分明确提示我们，导出函数已经由最初的4个变成了5个；在导出函数的描述部分也显示了新函数的导出序号、函数所在的RVA，以及新导出函数的名称TopXY，这意味着将私有函数转换为导出函数是成功的。你也可以新建一个测试程序对刚导出的函数进行测试，测试代码在随书文件列表chapter5\c\priFun.asm中。

# 5.6　小结

本章重点介绍了PE结构中的导出数据部分。通过对导出表的学习，读者能够了解导出表在PE中的作用，并从底层了解Windows加载程序修正导入表IAT的过程；同时，本章还对导出表的导出函数覆盖技术及私有函数导出做了简单的介绍。
